prt_church,100,124,5	script	ผู้ให้พร	10369,{
	// GM interaction
	if(getgmlevel())
	menu "ใช้งาน NPC",-,"เปิดการให้พร",L_Active,"ปิดการให้พร",L_Deactive;
	if($isAB >= 2){
		callsub L_Buff;
		end;
	}
	else{
		mes "วิธีใช้งาน: ในทุก ๆ อาทิตย์หาก <URL>แต้ม<INFO>https://playserver.in.th/index.php/Server/THE-BOX-5-91412</INFO></URL> มากกว่า " + callfunc("F_InsertComma",.pointRequired);
		mes " ^11B120ท่านจะได้รับ Increase AGI เป็นเวลา 1 วัน^000000";
		mes " ^11B120ท่านจะได้รับ Blessing เป็นเวลา 1 วัน^000000";
		mes " ^FF2929ทุก ๆ วันศุกร์ประมาณ 22.00-23.00น. พรจะหายไป และจะกลับมาอีกครั้งเมื่อ GM ตื่น และมาเช็คแต้ม^000000";
		.@price = .basePrice * BaseLevel;
		menu "ขอจ่ายด้วย " + callfunc("F_InsertComma",.@price) + "z แทน",-;
		if(Zeny >= .@price){
			Zeny = cap_value(Zeny - .@price,0,INT_MAX);
			callsub L_Buff;
		}
		close;
	}
	end;
	
	L_Buff:
	sc_start SC_INCREASEAGI,86400000,10;
	sc_start SC_BLESSING,86400000,10;
	sc_start SC_ANGELUS,86400000,10;
	sc_start SC_SLOWPOISON,86400000,4;
	sc_start SC_IMPOSITIO,86400000,5;
	sc_start SC_SUFFRAGIUM,86400000,3;
	sc_start SC_MAGNIFICAT,86400000,5;
	sc_start SC_GLORIA,86400000,5;
	return;
	L_Active:
	input .@i;
	.@i = cap_value(.@i,0,.maxChurch);
	$isAB = .@i;
	end;
	
	L_Deactive:
	$isAB = 0;
	end;
	
OnInit:
	.maxChurch = 2;
	.pointRequired = 5000;
	.basePrice = 10000;
	waitingroom "ผู้ให้พร",0;
	end;
}

-	script	ChurchHint	-1,{
	message strcharinfo(0),"สถานที่สำหรับรับพร";
	end;

OnInit:
	waitingroom "โบสถ์",0;
	end;
}

prontera,233,318,3	duplicate(ChurchHint)	ป้าย#ChurchHint1	837
prontera,238,313,3	duplicate(ChurchHint)	ป้าย#ChurchHint2	837

-	script	Pray	-1,{
	if(@isPraying
			|| (combo >= 100))
	end;

	if($isAB < 1){
		mes "วิธีใช้งาน: ในทุก ๆ อาทิตย์หาก <URL>แต้ม<INFO>https://playserver.in.th/index.php/Server/THE-BOX-5-91412</INFO></URL> มากกว่า " + callfunc("F_InsertComma",.pointRequired);
		mes " ^11B120ท่านจะสามารถ ภาวนา ได้ (เพิ่ม Combo)^000000";
		mes " ^FF2929ทุก ๆ วันศุกร์ประมาณ 22.00-23.00น. พรจะหายไป และจะกลับมาอีกครั้งเมื่อ GM ตื่น และมาเช็คแต้ม^000000";
		.@price = .basePrice * BaseLevel;
		menu "ขอจ่ายด้วย " + callfunc("F_InsertComma",.@price) + "z แทน",-;
		if(Zeny >= .@price){
			Zeny = cap_value(Zeny - .@price,0,INT_MAX);
		}
		else
		close;
	}
	
	@isPraying = 1;
	deltimer strnpcinfo(3) + "::OnPray";
	addtimer 3000, strnpcinfo(3) + "::OnPray";
	end;
	
OnPray:
	specialeffect2 325;
	percentheal 1,1;
	combo = cap_value(combo + 1,0,100);
	sc_end SC_EXPBOOST;
	sc_start SC_EXPBOOST,86400000,combo;
	sc_end SC_ITEMBOOST;
	sc_start SC_ITEMBOOST,86400000,combo;
	
	if((strcharinfo(3) == strnpcinfo(4))
			&& (combo < 100))
	addtimer 3000, strnpcinfo(3) + "::OnPray";
	else{
		@isPraying = 0;
		message strcharinfo(0),"คุณได้หยุดภาวนาแล้ว";
	}
	
	end;

OnInit:
	.pointRequired = 1500;
	.basePrice = 1000;
	waitingroom "ภาวนา",0;
	end;
}

prt_church,108,122,5	duplicate(Pray)	ภาวนา#Pray1	2035
prt_church,90,122,5	duplicate(Pray)	ภาวนา#Pray2	2035
