force_3-3	mapflag	pvp
force_3-3	mapflag	novending
force_3-3	mapflag	nochat
force_3-3	mapflag	nowarp
force_3-3	mapflag	nowarpto
force_3-3	mapflag	nosave
force_3-3	mapflag	nobranch
force_3-3	mapflag	notomb

prontera,155,180,5	script	เล่น	736,{
	// Random save position
	savepoint "prontera",156,191,5,5;
	// Guild check
	if(!getcharid(2)){
		mes "โปรดสร้าง Guild หรือเข้าร่วม Guild";
		next;
		menu "เข้าร่วม Guild",-,"สร้าง Guild",OnGuildMaking;
		mes "ลองพูดคุยกับผู้เล่นอื่นดู แล้วขอเข้าร่วม Guild";
		close;
	}
	// Penalty for exploit users
	if(!isSafeReturn){
		mes "คุณกลับเมืองด้วยวิธีอื่นที่ไม่ใช่การใช้ @load, Return Orb หรือการตาย คุณต้องตายเดี๋ยวนี้";
		next;
		menu "โอเค",-;
		close2;
		atcommand "@die";
		end;
	}
	// Get highest floor level of players
	.@highestLevel = max(1,min($maximumFloor,highestLevel));
	mes "โปรดระบุระดับ 1~" + .@highestLevel;
	// Players floor selection
	input .@floor;
	// Checking players selected floor conditions
	if((.@floor <= 0)
			|| (.@floor > .@highestLevel)
			|| (.@floor > $maximumFloor)){
		mes "ระดับที่ระบุผิดพลาด";
		close;
	}
	.@playingMapSize = getarraysize(.playingMaps$);
	// Check that floor had map yet?
	if(.playingMaps$[(.@floor - 1)] == ""){
		freeloop(1);
		// Random map
		.@randomMap$ = .maps$[rand(0,(.mapSize - 1))];
		setarray .playingMaps$[.@floor - 1],.@randomMap$;
		// Check map is using
		for(.@i = 0; .@i < .@playingMapSize; .@i++){
			if(.@i == (.@floor - 1))
			continue;
			
			// Hit
			if(.@randomMap$ == .playingMaps$[.@i]){
				// Restart checking
				.@i = 0;
				// Random map
				.@randomMap$ = .maps$[rand(0,(.mapSize - 1))];
				setarray .playingMaps$[(.@floor - 1)],.@randomMap$;
			}
		}
		freeloop(0);
	}
	.@tier = callsub(OnGetTier,.@floor);
	// Check kill amount
	if(!.killAmount[(.@floor - 1)]){
		setarray .killAmount[(.@floor - 1)],.maximumKillAmount[.@tier];
		setarray .spawnedAmount[(.@floor - 1)],0;
		// Setup 4 monster types
		for(.@i = 0; .@i < 4; .@i++)
		setarray getd(".monsterType" + .@floor + "["+ .@i +"]"),(.@tier == 0) ? $attackableMonsterTier1Ids[rand(0,$attackableMonsterTier1IdSize - 1)] : (.@tier == 1) ? $attackableMonsterTier2Ids[rand(0,$attackableMonsterTier2IdSize - 1)] : (.@tier == 2) ? $attackableMonsterTier3Ids[rand(0,$attackableMonsterTier3IdSize - 1)] : (.@tier == 3) ? $attackableMonsterTier4Ids[rand(0,$attackableMonsterTier4IdSize - 1)] : (.@tier == 4) ? $attackableMonsterTier5Ids[rand(0,$attackableMonsterTier5IdSize - 1)] : $attackableMonsterTier6Ids[rand(0,$attackableMonsterTier6IdSize - 1)];
	}
	@floor = .@floor;
	// Add drop bonus
	sc_end SC_ITEMBOOST;
	sc_start SC_ITEMBOOST,86400000,(.@floor / 2);
	// Debuff at floor 97, 98, 99 and 100
	if(.@floor >= 97)
	atcommand "@bd " + (5 - (100 - .@floor));
	// Going to play maps
	isSafeReturn = 0;
	// Block
	setpcblock PCBLOCK_ALL,true;
	setpcblock PCBLOCK_MOVE,false;
	addtimer 2000,strnpcinfo(3) + "::OnWarp";
	// Warp now
	warp .playingMaps$[(.@floor - 1)],0,0;
	// Spawn monster if needed
	callsub OnSpawnMonster,.@floor;
	end;
	
OnGuildMaking:
	mes "ชื่อ Guild";
	input .@guildName$;
	atcommand "@guild " + .@guildName$;
	addtimer 100,strnpcinfo(3) + "::OnGuildLevelUp";
	close;
	
OnGuildLevelUp:
	atcommand "@guildlvup 99";
	end;
	
OnWarp:
	setpcblock PCBLOCK_ALL,false;
	end;
	
OnSpawnMonster:
	.@floor = getarg(0,-1);
	.@monsterRank = getarg(1,1);
	.@tier = callsub(OnGetTier,.@floor);
	// Unexpected error
	if(.@floor <= 0)
	end;
	// Loop spawn
	for(.@i = 0; .@i < .maximumSpawnAmount[.@tier]; .@i++){
		// Already max
		if(.spawnedAmount[(.@floor - 1)] >= .maximumSpawnAmount[.@tier])
		break;
		
		// Spawn
		monster .playingMaps$[(.@floor - 1)],0,0,"--ja--",getd(".monsterType" + .@floor + "[" + rand(0,3) + "]"),1,strnpcinfo(0) + "::OnMonsterDead";
		// HP ((50 * Floor) * Floor) * Floor (Floor%) * Floor ((Floor / 2)%))
		.@hp = (50 * .@floor) * .@floor * ((rand(0,100) <= .@floor) ? .@floor : 1) * ((rand(0,100) <= (.@floor / 2)) ? .@floor : 1);
		// Capping
		.@hp = cap_value(.@hp,50,(INT_MAX - 1));
		// Set new drop amount
		setunitdata $@mobid[0],UMOB_DROP_AMOUNT,$orbDropAmount[.@tier];
		// Set new rank
		setunitdata $@mobid[0],UMOB_DYNAMIC,.@monsterRank;
		// Set new hp
		setunitdata $@mobid[0],UMOB_HP,.@hp;
		setunitdata $@mobid[0],UMOB_MAXHP,.@hp;
		// Increase spawned amount
		setarray .spawnedAmount[(.@floor - 1)],(.spawnedAmount[(.@floor - 1)] + 1);
	}
	
	end;

OnMonsterDead:
	if(!playerattached())
	end;

	setarray .spawnedAmount[(@floor - 1)],cap_value(.spawnedAmount[(@floor - 1)] - 1,0,INT_MAX);
	setarray .killAmount[(@floor - 1)],cap_value(.killAmount[(@floor - 1)] - 1,0,INT_MAX);
	
	if(.killAmount[(@floor - 1)])
	announce "Boss จะปรากฎเมื่อกำจัด Monster อีก " + .killAmount[(@floor - 1)] + " ตัว",bc_area,0xE4A827;
	else if(!.bossSpawn[(@floor - 1)]){
		.@tier = callsub(OnGetTier,@floor);
		setarray .bossSpawn[(@floor - 1)],1;
		// Random position in current map
		getfreecell strcharinfo(3),.@x,.@y;
		// Mark on minimap
		viewpointmap strcharinfo(3),1,.@x,.@y,1,0xFF6D6D;
		// Spawn boss now
		monster .playingMaps$[(@floor - 1)],.@x,.@y,"Boss",getd(".monsterType" + @floor + "["+ rand(0,3) +"]"),1,strnpcinfo(0) + "::OnBossDead",2;
		// HP ((50 * Floor) * Floor) * Floor))
		.@hp = (50 * @floor) * @floor * @floor;
		// Capping
		.@hp = cap_value(.@hp,50,(INT_MAX - 1));
		// Last Boss
		.@isLastBoss = (@floor >= 100);
		if(.@isLastBoss)
		.@hp = 2000000000;
		// Set mode
		setunitdata $@mobid[0],UMOB_MODE,MD_CANMOVE|MD_AGGRESSIVE|MD_CASTSENSORIDLE|MD_CANATTACK|MD_CASTSENSORCHASE|MD_CHANGECHASE|MD_CHANGETARGETMELEE|MD_CHANGETARGETCHASE|MD_RANDOMTARGET|MD_MVP|MD_KNOCKBACKIMMUNE|MD_TELEPORTBLOCK|MD_DETECTOR|MD_STATUSIMMUNE;
		// Set new damage taken (100 - Floor)
		setunitdata $@mobid[0],UMOB_DAMAGETAKEN,(.@isLastBoss ? 1 : (100 - @floor));
		// Set new drop amount
		setunitdata $@mobid[0],UMOB_DROP_AMOUNT,$orbDropAmount[.@tier];
		// Set new rank
		setunitdata $@mobid[0],UMOB_DYNAMIC,(.@isLastBoss ? 100 : (1 + (@floor / 10)));
		// Set new hp
		setunitdata $@mobid[0],UMOB_HP,.@hp;
		setunitdata $@mobid[0],UMOB_MAXHP,.@hp;
		
		announce "Boss ปรากฎแล้ว! (ดูตำแหน่งได้ที่ Minimap)",bc_area,0xE4A827;
	}
	// If spawn amount < 5 == Respawn immediately
	if(.spawnedAmount[(@floor - 1)] < 5)
	callsub OnSpawnMonster,@floor;
	// If spawn amount < 50 == 10% Chance to respawn
	else if((.spawnedAmount[(@floor - 1)] < 50)
			&& (rand(0,100) < 10))
	callsub OnSpawnMonster,@floor;
	end;
	
OnBossDead:
	if(!playerattached())
	end;

	// Mark boss as dead
	setarray .bossSpawn[(@floor - 1)],0;
	// Reset kill amount
	.@tier = callsub(OnGetTier,@floor);
	setarray .killAmount[(@floor - 1)],.maximumKillAmount[.@tier];
	// Random new 4 monster types
	for(.@i = 0; .@i < 4; .@i++)
	setarray getd(".monsterType" + @floor + "["+ .@i +"]"),(.@tier == 0) ? $attackableMonsterTier1Ids[rand(0,$attackableMonsterTier1IdSize - 1)] : (.@tier == 1) ? $attackableMonsterTier2Ids[rand(0,$attackableMonsterTier2IdSize - 1)] : (.@tier == 2) ? $attackableMonsterTier3Ids[rand(0,$attackableMonsterTier3IdSize - 1)] : (.@tier == 3) ? $attackableMonsterTier4Ids[rand(0,$attackableMonsterTier4IdSize - 1)] : (.@tier == 4) ? $attackableMonsterTier5Ids[rand(0,$attackableMonsterTier5IdSize - 1)] : $attackableMonsterTier6Ids[rand(0,$attackableMonsterTier6IdSize - 1)];
	
	// Get (Floor * 10) Dismantle Points
	getitem 10000007,(@floor * 10);
	// Get (Floor * 5) Soul Orb
	getitem 10000002,(@floor * 5);
	// Get (Floor * 2) Body Orb
	getitem 10000003,(@floor * 2);
	// Get (Floor * 1) Mind Orb
	getitem 10000004,@floor;
	// (0.5 * Floor)% Chance to get 1 Refine Orb
	.@refineOrbChance = rand(0,1000);
	.@refineOrbTarget = (@floor * 5);
	if(.@refineOrbChance < .@refineOrbTarget)
	getitem 10000005,1;
	else
	dispbottom "ต้องสุ่มได้ " + .@refineOrbTarget + " เพื่อที่จะได้รับ 1 " + getitemname(10000005) + " คุณสุ่มได้ " + .@refineOrbChance;
	// (0.1 * Floor)% Chance to get 1 Grade Orb
	.@gradeOrbChance = rand(0,1000);
	.@gradeOrbTarget = @floor;
	if(.@gradeOrbChance < .@refineOrbTarget)
	getitem 10000006,1;
	else
	dispbottom "ต้องสุ่มได้ " + .@gradeOrbTarget + " เพื่อที่จะได้รับ 1 " + getitemname(10000006) + " คุณสุ่มได้ " + .@gradeOrbChance;

	// Announce
	announce "Boss ชั้น " + @floor + " ถูกกำจัดโดย " + strcharinfo(0)+ " (ผู้กำจัดจะได้รางวัลพิเศษเพียงคนเดียว) แต่ทุกคนภายในชั้นดังกล่าวจะสามารถไปยังชั้น " + (@floor + 1) + " ได้แล้ว",bc_all,0xF9FF40;
	// Store floor to rewarded
	.rewardFloor = (@floor + 1);
	addrid(1);
	if(highestLevel < .rewardFloor)
	highestLevel = .rewardFloor;
	specialeffect2 518;
	end;

OnGetTier:
	.@floor = getarg(0,1);
	return (.@floor < 10) ? 0 : (.@floor < 20) ? 1 : (.@floor < 30) ? 2 : (.@floor < 50) ? 3 : (.@floor < 75) ? 4 : 5;
	
OnInit:
	waitingroom "เล่น",0;
	// Maximum floor
	$maximumFloor = 100;
	// Kill amount per floor to spawn big monster {<10<20<30<50<75<100}
	setarray .maximumKillAmount[0],50,100,200,300,400,500;
	// Max spawned amount per floor {<10<20<30<50<75<100}
	setarray .maximumSpawnAmount[0],50,75,100,125,150,175;
	// Available maps
	setarray .maps$[0],"gef_fild00","gef_fild01","gef_fild02","gef_fild03","gef_fild04","gef_fild05","gef_fild06","gef_fild07","gef_fild08","gef_fild09","gef_fild10","gef_fild11","gef_fild12","gef_fild13","gef_fild14","moc_fild01","moc_fild02","moc_fild03","moc_fild07","moc_fild11","moc_fild12","moc_fild13","moc_fild16","moc_fild17","moc_fild18","moc_fild19","pay_fild01","pay_fild02","pay_fild03","pay_fild04","pay_fild05","pay_fild06","pay_fild07","pay_fild08","pay_fild09","pay_fild10","pay_fild11","prt_fild00","prt_fild01","prt_fild02","prt_fild03","prt_fild04","prt_fild05","prt_fild06","prt_fild07","prt_fild08","prt_fild09","prt_fild10","prt_fild11","xmas_fild01","cmd_fild01","cmd_fild02","cmd_fild03","cmd_fild04","cmd_fild05","cmd_fild06","cmd_fild07","cmd_fild08","cmd_fild09","yuno_fild01","yuno_fild02","yuno_fild03","yuno_fild04","ama_fild01","gon_fild01","um_fild01","um_fild02","um_fild03","um_fild04","nif_fild01","nif_fild02","lou_fild01","ayo_fild01","ayo_fild02","yuno_fild05","yuno_fild07","yuno_fild08","yuno_fild09","yuno_fild11","yuno_fild12","ein_fild06","ein_fild07","ein_fild08","ein_fild09","ein_fild10","ein_fild03","ein_fild04","lhz_fild02","lhz_fild03","lhz_fild01","hu_fild07","hu_fild05","hu_fild04","hu_fild01","yuno_fild06","hu_fild02","hu_fild03","hu_fild06","ein_fild01","ein_fild02","ein_fild05","yuno_fild10","ra_fild01","ra_fild02","ra_fild03","ra_fild04","ra_fild05","ra_fild06","ra_fild07","ra_fild08","ra_fild09","ra_fild10","ra_fild11","ra_fild12","ra_fild13","ve_fild01","ve_fild02","ve_fild03","ve_fild04","ve_fild05","ve_fild07","mosk_fild02","moc_fild20","moc_fild21","moc_fild22","man_fild01","man_fild03","spl_fild02","spl_fild03","moc_fild22b","man_fild02","spl_fild01","bra_fild01","dic_fild01","dic_fild02","bif_fild01","bif_fild02","dew_fild01","ma_fild01","ma_fild02","ecl_fild01","prt_fild08a","prt_fild08b","prt_fild08c","prt_fild08d","lasa_fild01","lasa_fild02","tra_fild","gw_fild01","gw_fild02";
	.mapSize = getarraysize($maps$);
	end;
}