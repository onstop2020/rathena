prontera,155,180,5	script	เล่น	736,{
	atcommand "@debuff 0";
	atcommand "@bd 0";
	// Random save position
	savepoint "prontera",156,191,5,5;
	// Guild check
	if(!getcharid(2)){
		mes "โปรดสร้าง Guild หรือเข้าร่วม Guild";
		next;
		menu "เข้าร่วม Guild",-,"สร้าง Guild",OnGuildMaking;
		mes "ลองพูดคุยกับผู้เล่นอื่นดู แล้วขอเข้าร่วม Guild";
		close;
	}
	// Penalty for exploit users
	if(!isSafeReturn){
		mes "คุณกลับเมืองด้วยวิธีอื่นที่ไม่ใช่การใช้ @load, Return Orb หรือการตาย คุณต้องตายเดี๋ยวนี้";
		next;
		menu "โอเค",-;
		close2;
		atcommand "@die";
		end;
	}
	// Get highest floor level of players
	.@highestLevel = max(1,min($maximumFloor,highestLevel));
	mes "โปรดระบุระดับ 1~" + .@highestLevel + " (ระบุ 0 เช็คคน)";
	if(.@highestLevel >= 10)
	mes "^FF4040ภายใน 10 นาทีนี้ GvG Map ที่เป็นเลข" + (.isEvenPvP ? "คู่" : "คี่") + "^000000";
	mes "---";
	.@capHp = cap_value(.@highestLevel * 2000000,50,(INT_MAX - 1));
	.@minHp = cap_value(((50 * .@highestLevel) * .@highestLevel),50,.@capHp);
	.@medHp = cap_value(((50 * .@highestLevel) * .@highestLevel * .@highestLevel),50,.@capHp);
	.@maxHp = cap_value(((50 * .@highestLevel) * .@highestLevel * .@highestLevel * .@highestLevel),50,.@capHp);
	mes "HP ของ Monster ระดับ " + .@highestLevel + " \nคือ ^FF4040" + callfunc("F_InsertComma",.@minHp) + "^000000 \nหรือ ^FF4040" + callfunc("F_InsertComma",.@medHp) + "^000000 \nหรือ ^FF4040" + callfunc("F_InsertComma",.@maxHp);
	.@tier = callsub(OnGetTier,.@highestLevel);
	mes "---";
	mes "Monster ระดับ " + .@highestLevel + " จะ Drop ^3629FF" + $orbDropAmount[.@tier] + "^000000 ชิ้น";
	mes "---";
	mes "ทุก ๆ การกำจัด Boss ในระดับที่หารด้วย 5 ลงตัวจะมีโอกาส ^3629FF" + .specialRebornSkillChance + "%^000000 ที่จะรับ^3629FFจำนวน Skill พิเศษ^000000 (รับได้รอบเดียวต่อระดับ)";
	if(.@highestLevel >= 97){
		mes "---";
		mes "ระดับ " + .@highestLevel + " ^FF4040คุณจะโดนหาร Bonus จากอุปกรณ์สวมใส่ " + (5 - (100 - .@highestLevel)) + "^000000";
	}
	// Players floor selection
	input .@floor;
	// Checking players selected floor conditions
	if((.@floor <= 0)
			|| (.@floor > .@highestLevel)
			|| (.@floor > $maximumFloor)){
		next;
		freeloop(1);
		// Loop playing map
		.@playingMapSize = getarraysize($playingMaps$);
		for(.@i = 0; .@i < .@playingMapSize; .@i++){
			if(($playingMaps$[.@i] == "")
					|| ($playingMaps$[.@i] == " "))
			continue;
			
			mes "ชั้น " + $playingMapsFloor[.@i] + ": " + getmapusers($playingMaps$[.@i]) + " คน";
		}
		if(!.@playingMapSize)
		mes "ไม่พบผู้เล่นใด ๆ ในทุกระดับ";
		
		freeloop(0);
		close;
	}
	.@playingMapSize = getarraysize($playingMaps$);
	// Check that floor had map yet?
	if(($playingMaps$[(.@floor - 1)] == "") || ($playingMaps$[(.@floor - 1)] == " ")){
		freeloop(1);
		// Random map
		.@randomMap$ = $maps$[rand(0,($mapSize - 1))];
		setarray $playingMaps$[.@floor - 1],.@randomMap$;
		setarray $playingMapsFloor[.@floor - 1],.@floor;
		// Check map is using
		for(.@i = 0; .@i < .@playingMapSize; .@i++){
			if(.@i == (.@floor - 1))
			continue;
			
			// Hit
			if(.@randomMap$ == $playingMaps$[.@i]){
				// Restart checking
				.@i = 0;
				// Random map
				.@randomMap$ = $maps$[rand(0,($mapSize - 1))];
				setarray $playingMaps$[(.@floor - 1)],.@randomMap$;
			}
		}
		freeloop(0);
	}
	.@tier = callsub(OnGetTier,.@floor);
	// Check kill amount
	if(!.killAmount[(.@floor - 1)]){
		setarray .killAmount[(.@floor - 1)],.maximumKillAmount[.@tier];
		setarray .spawnedAmount[(.@floor - 1)],0;
		// Setup 4 monster types
		for(.@i = 0; .@i < 4; .@i++)
		setarray getd(".monsterType" + .@floor + "["+ .@i +"]"),(.@tier == 0) ? $attackableMonsterTier1Ids[rand(0,$attackableMonsterTier1IdSize - 1)] : (.@tier == 1) ? $attackableMonsterTier2Ids[rand(0,$attackableMonsterTier2IdSize - 1)] : (.@tier == 2) ? $attackableMonsterTier3Ids[rand(0,$attackableMonsterTier3IdSize - 1)] : (.@tier == 3) ? $attackableMonsterTier4Ids[rand(0,$attackableMonsterTier4IdSize - 1)] : (.@tier == 4) ? $attackableMonsterTier5Ids[rand(0,$attackableMonsterTier5IdSize - 1)] : $attackableMonsterTier6Ids[rand(0,$attackableMonsterTier6IdSize - 1)];
	}
	@floor = .@floor;
	// Set map flag
	setmapflag $playingMaps$[(.@floor - 1)],MF_NOVENDING;
	setmapflag $playingMaps$[(.@floor - 1)],MF_NOCHAT;
	setmapflag $playingMaps$[(.@floor - 1)],MF_NOWARP;
	setmapflag $playingMaps$[(.@floor - 1)],MF_NOWARPTO;
	setmapflag $playingMaps$[(.@floor - 1)],MF_NOSAVE;
	setmapflag $playingMaps$[(.@floor - 1)],MF_NOBRANCH;
	setmapflag $playingMaps$[(.@floor - 1)],MF_NOTOMB;
	if((.@floor >= 10)
			&& ((.@floor % 2) == (.isEvenPvP ? 0 : 1))){
		setmapflag $playingMaps$[(.@floor - 1)],MF_GVG;
		setmapflag $playingMaps$[(.@floor - 1)],MF_GVG_NOPARTY;
	}
	// Add drop bonus
	sc_end SC_ITEMBOOST;
	sc_start SC_ITEMBOOST,86400000,(.@floor / 2);
	// Add debuff a bit
	atcommand "@debuff " + cap_value((.@floor < 10) ? (.@floor - 1) : (.@floor < 20) ? (.@floor - 10) : (.@floor < 30) ? (.@floor - 20) : (.@floor < 50) ? (.@floor - 30) : (.@floor < 75) ? (.@floor - 50) : (.@floor - 75),0,50);
	// Bonus divider at floor 97, 98, 99 and 100
	if(.@floor >= 97)
	atcommand "@bd " + (5 - (100 - .@floor));
	// Going to play maps
	isSafeReturn = 0;
	// Block
	setpcblock PCBLOCK_ALL,true;
	setpcblock PCBLOCK_MOVE,false;
	addtimer 2000,strnpcinfo(3) + "::OnWarp";
	// Warp now
	warp $playingMaps$[(.@floor - 1)],0,0;
	// Spawn monster if needed
	callsub OnSpawnMonster,.@floor;
	end;
	
OnGuildMaking:
	mes "ชื่อ Guild";
	input .@guildName$;
	atcommand "@guild " + .@guildName$;
	addtimer 100,strnpcinfo(3) + "::OnGuildLevelUp";
	close;
	
OnGuildLevelUp:
	atcommand "@guildlvup 99";
	end;
	
OnWarp:
	setpcblock PCBLOCK_ALL,false;
	end;
	
OnSpawnMonster:
	.@floor = getarg(0,-1);
	.@monsterRank = getarg(1,1);
	.@tier = callsub(OnGetTier,.@floor);
	// Unexpected error
	if(.@floor <= 0)
	end;
	// Loop spawn
	for(.@i = 0; .@i < .maximumSpawnAmount[.@tier]; .@i++){
		// Already max
		if(.spawnedAmount[(.@floor - 1)] >= .maximumSpawnAmount[.@tier])
		break;
		// Already max
		if(mobcount($playingMaps$[(.@floor - 1)],"all") >= .maximumSpawnAmount[.@tier])
		break;
		
		// Spawn
		monster $playingMaps$[(.@floor - 1)],0,0,"--ja--",getd(".monsterType" + .@floor + "[" + rand(0,3) + "]"),1,strnpcinfo(0) + "::OnMonsterDead";
		// HP ((50 * Floor) * Floor) * Floor (Floor%) * Floor ((Floor / 2)%))
		// Capping
		.@capHp = cap_value(.@floor * 2000000,50,(INT_MAX - 1));
		.@hp = cap_value(((50 * .@floor) * .@floor * ((rand(0,100) <= .@floor) ? .@floor : 1) * ((rand(0,100) <= (.@floor / 2)) ? .@floor : 1)),50,.@capHp);
		// Set new drop amount
		setunitdata $@mobid[0],UMOB_DROP_AMOUNT,$orbDropAmount[.@tier];
		// Set new rank
		setunitdata $@mobid[0],UMOB_DYNAMIC,.@monsterRank;
		// Set new hp
		setunitdata $@mobid[0],UMOB_HP,.@hp;
		setunitdata $@mobid[0],UMOB_MAXHP,.@hp;
		// Increase spawned amount
		setarray .spawnedAmount[(.@floor - 1)],(.spawnedAmount[(.@floor - 1)] + 1);
	}
	
	end;

OnMonsterDead:
	if(!playerattached())
	end;

	setarray .spawnedAmount[(@floor - 1)],cap_value(.spawnedAmount[(@floor - 1)] - 1,0,INT_MAX);
	setarray .killAmount[(@floor - 1)],cap_value(.killAmount[(@floor - 1)] - 1,0,INT_MAX);
	
	if(.killAmount[(@floor - 1)])
	announce "Boss จะปรากฎเมื่อกำจัด Monster อีก " + .killAmount[(@floor - 1)] + " ตัว",bc_area,0xE4A827;
	else if(!.bossSpawn[(@floor - 1)]){
		.@tier = callsub(OnGetTier,@floor);
		setarray .bossSpawn[(@floor - 1)],1;
		// Random position in current map
		getfreecell strcharinfo(3),.@x,.@y;
		// Mark on minimap
		viewpointmap strcharinfo(3),1,.@x,.@y,1,0xFF6D6D;
		// Spawn boss now
		monster $playingMaps$[(@floor - 1)],.@x,.@y,"Boss",getd(".monsterType" + @floor + "["+ rand(0,3) +"]"),1,strnpcinfo(0) + "::OnBossDead",2;
		// HP ((50 * Floor) * Floor) * Floor))
		// Capping
		.@hp = cap_value(((50 * @floor) * @floor * @floor),50,.maximumHp);
		// Last Boss
		.@isLastBoss = (@floor >= 100);
		// Set mode
		setunitdata $@mobid[0],UMOB_MODE,MD_CANMOVE|MD_AGGRESSIVE|MD_CASTSENSORIDLE|MD_CANATTACK|MD_CASTSENSORCHASE|MD_CHANGECHASE|MD_CHANGETARGETMELEE|MD_CHANGETARGETCHASE|MD_RANDOMTARGET|MD_MVP|MD_KNOCKBACKIMMUNE|MD_TELEPORTBLOCK|MD_DETECTOR|MD_STATUSIMMUNE;
		// Set new damage taken (100 - Floor)
		setunitdata $@mobid[0],UMOB_DAMAGETAKEN,(.@isLastBoss ? 1 : (100 - @floor));
		// Set new drop amount
		setunitdata $@mobid[0],UMOB_DROP_AMOUNT,$orbDropAmount[.@tier];
		// Set new rank
		setunitdata $@mobid[0],UMOB_DYNAMIC,(.@isLastBoss ? 100 : (1 + (@floor / 10)));
		// Set new hp
		setunitdata $@mobid[0],UMOB_HP,.@hp;
		setunitdata $@mobid[0],UMOB_MAXHP,.@hp;
		
		announce "Boss ปรากฎแล้ว! (ดูตำแหน่งได้ที่ Minimap)",bc_area,0xE4A827;
	}
	// If spawn amount < 5 == Respawn immediately
	if(.spawnedAmount[(@floor - 1)] < 5)
	callsub OnSpawnMonster,@floor;
	// If spawn amount < 50 == 10% Chance to respawn
	else if((.spawnedAmount[(@floor - 1)] < 50)
			&& (rand(0,100) < 10))
	callsub OnSpawnMonster,@floor;
	end;
	
OnBossDead:
	if(!playerattached())
	end;

	// Every 5 floor there are chance to increase special reborn skill
	if(((@floor % 5) == 0) && !getd("specialRebornSkill" + @floor)){
		.@specialRebornSkillRand = rand(0,100);
		if(.@specialRebornSkillRand <= .specialRebornSkillChance){
			set getd("specialRebornSkill" + @floor),1;
			specialRebornSkill++;
			message strcharinfo(0),"คุณได้รับจำนวน Skill พิเศษ!";
		}
		else
		message strcharinfo(0),"ต้องสุ่มได้ " + .specialRebornSkillChance + " คุณสุ่มได้ " + .@specialRebornSkillRand + " เพื่อรับจำนวน Skill พิเศษ";
	}

	// Mark boss as dead
	setarray .bossSpawn[(@floor - 1)],0;
	// Reset kill amount
	.@tier = callsub(OnGetTier,@floor);
	setarray .killAmount[(@floor - 1)],.maximumKillAmount[.@tier];
	// Random new 4 monster types
	for(.@i = 0; .@i < 4; .@i++)
	setarray getd(".monsterType" + @floor + "["+ .@i +"]"),(.@tier == 0) ? $attackableMonsterTier1Ids[rand(0,$attackableMonsterTier1IdSize - 1)] : (.@tier == 1) ? $attackableMonsterTier2Ids[rand(0,$attackableMonsterTier2IdSize - 1)] : (.@tier == 2) ? $attackableMonsterTier3Ids[rand(0,$attackableMonsterTier3IdSize - 1)] : (.@tier == 3) ? $attackableMonsterTier4Ids[rand(0,$attackableMonsterTier4IdSize - 1)] : (.@tier == 4) ? $attackableMonsterTier5Ids[rand(0,$attackableMonsterTier5IdSize - 1)] : $attackableMonsterTier6Ids[rand(0,$attackableMonsterTier6IdSize - 1)];
	
	// Get (Floor * 10) Dismantle Points
	getitem 10000007,(@floor * 10);
	// Get (Floor * 5) Soul Orb
	getitem 10000002,(@floor * 5);
	// Get (Floor * 2) Body Orb
	getitem 10000003,(@floor * 2);
	// Get (Floor * 1) Mind Orb
	getitem 10000004,@floor;
	// (0.5 * Floor)% Chance to get 1 Refine Orb
	.@refineOrbChance = rand(0,1000);
	.@refineOrbTarget = (@floor * 5);
	if(.@refineOrbChance < .@refineOrbTarget)
	getitem 10000005,1;
	else
	dispbottom "ต้องสุ่มได้ " + .@refineOrbTarget + " เพื่อที่จะได้รับ 1 " + getitemname(10000005) + " คุณสุ่มได้ " + .@refineOrbChance;
	// (0.1 * Floor)% Chance to get 1 Grade Orb
	.@gradeOrbChance = rand(0,1000);
	.@gradeOrbTarget = @floor;
	if(.@gradeOrbChance < .@refineOrbTarget)
	getitem 10000006,1;
	else
	dispbottom "ต้องสุ่มได้ " + .@gradeOrbTarget + " เพื่อที่จะได้รับ 1 " + getitemname(10000006) + " คุณสุ่มได้ " + .@gradeOrbChance;

	// Announce
	announce "Boss ชั้น " + @floor + " ถูกกำจัดโดย " + strcharinfo(0)+ " (ผู้กำจัดจะได้รางวัลพิเศษเพียงคนเดียว) แต่ทุกคนภายในชั้นดังกล่าวจะสามารถไปยังชั้น " + (@floor + 1) + " ได้แล้ว",bc_all,0xF9FF40;
	
	if(@floor < 100){
		// Event Area (25%)
		if(rand(100) < 25)
		callsub OnEventArea;
		// Event Boss (25%)
		else if(rand(100) < 25)
		callsub OnEventBoss;
	}
	
	// Store floor to rewarded
	.rewardFloor = (@floor + 1);
	addrid(1);
	if(highestLevel < .rewardFloor)
	highestLevel = .rewardFloor;
	specialeffect2 518;
	end;

OnEventBoss:
	.@tier = callsub(OnGetTier,@floor);
	// Random position in current map
	getfreecell strcharinfo(3),.@x,.@y;
	// Mark on minimap
	viewpointmap strcharinfo(3),1,.@x,.@y,1,0xA86DBB;
	// Spawn boss now
	monster $playingMaps$[(@floor - 1)],.@x,.@y,"Event Boss",$attackableMonsterIds[rand($attackableMonsterIdSize)],1,strnpcinfo(0) + "::OnEventBossDead",2;
	// HP ((50 * Floor) * Floor) * Floor))
	// Capping
	.@hp = cap_value(((50 * @floor) * @floor * @floor),50,.maximumHp);
	// Set mode
	setunitdata $@mobid[0],UMOB_MODE,MD_CANMOVE|MD_AGGRESSIVE|MD_CASTSENSORIDLE|MD_CANATTACK|MD_CASTSENSORCHASE|MD_CHANGECHASE|MD_CHANGETARGETMELEE|MD_CHANGETARGETCHASE|MD_RANDOMTARGET|MD_MVP|MD_KNOCKBACKIMMUNE|MD_TELEPORTBLOCK|MD_DETECTOR|MD_STATUSIMMUNE;
	// Set new damage taken (100 - Floor)
	setunitdata $@mobid[0],UMOB_DAMAGETAKEN,rand(1,100);
	// Set new drop amount
	setunitdata $@mobid[0],UMOB_DROP_AMOUNT,$orbDropAmount[.@tier];
	// Set new rank
	setunitdata $@mobid[0],UMOB_DYNAMIC,rand(1,100);
	// Set new hp
	setunitdata $@mobid[0],UMOB_HP,.@hp;
	setunitdata $@mobid[0],UMOB_MAXHP,.@hp;
	
	announce "Event Boss ปรากฎแล้ว! (ดูตำแหน่งได้ที่ Minimap)",bc_area,0xA86DBB;
	return;

OnEventBossDead:
	// Get (Floor * 20) Dismantle Points
	getitem 10000007,(@floor * 20);
	// Get (Floor * 10) Soul Orb
	getitem 10000002,(@floor * 10);
	// Get (Floor * 4) Body Orb
	getitem 10000003,(@floor * 4);
	// Get (Floor * 2) Mind Orb
	getitem 10000004,(@floor * 2);
	// (0.5 * Floor)% Chance to get 1 Refine Orb
	.@refineOrbChance = rand(0,1000);
	.@refineOrbTarget = (@floor * 5);
	if(.@refineOrbChance < .@refineOrbTarget)
	getitem 10000005,1;
	else
	dispbottom "ต้องสุ่มได้ " + .@refineOrbTarget + " เพื่อที่จะได้รับ 1 " + getitemname(10000005) + " คุณสุ่มได้ " + .@refineOrbChance;
	// (0.1 * Floor)% Chance to get 1 Grade Orb
	.@gradeOrbChance = rand(0,1000);
	.@gradeOrbTarget = @floor;
	if(.@gradeOrbChance < .@refineOrbTarget)
	getitem 10000006,1;
	else
	dispbottom "ต้องสุ่มได้ " + .@gradeOrbTarget + " เพื่อที่จะได้รับ 1 " + getitemname(10000006) + " คุณสุ่มได้ " + .@gradeOrbChance;

	// Announce
	announce "Event Boss ชั้น " + @floor + " ถูกกำจัดโดย " + strcharinfo(0)+ " (ผู้กำจัดจะได้รางวัลพิเศษเพียงคนเดียว)",bc_all,0xA86DBB;
	end;

OnEventArea:
	.@tier = callsub(OnGetTier,@floor);
	// Random position in current map
	getfreecell strcharinfo(3),.@x,.@y;
	// Mark on minimap
	viewpointmap strcharinfo(3),1,.@x,.@y,1,0xA86DBB;
	.@x1 = .@x - 10;
	.@x2 = .@x + 10;
	.@y1 = .@y - 10;
	.@y2 = .@y + 10;
	freeloop(1);
	for(.@i = 0; .@i < .eventAreaAmount; .@i++){
		// Spawn monster now
		areamonster $playingMaps$[(@floor - 1)],.@x1,.@y1,.@x2,.@y2,"Event Area",$attackableMonsterIds[rand($attackableMonsterIdSize)],1,strnpcinfo(0) + "::OnEventAreaDead",2;
		// HP ((50 * Floor) * Floor) * Floor (Floor%) * Floor ((Floor / 2)%))
		// Capping
		.@capHp = cap_value(@floor * 2000000,50,(INT_MAX - 1));
		.@hp = cap_value(((50 * @floor) * @floor * ((rand(0,100) <= @floor) ? @floor : 1) * ((rand(0,100) <= (@floor / 2)) ? @floor : 1)),50,.@capHp);
		// Set mode
		setunitdata $@mobid[0],UMOB_MODE,MD_CANMOVE|MD_AGGRESSIVE|MD_CASTSENSORIDLE|MD_CANATTACK|MD_CASTSENSORCHASE|MD_CHANGECHASE|MD_CHANGETARGETMELEE|MD_CHANGETARGETCHASE|MD_RANDOMTARGET|MD_MVP|MD_KNOCKBACKIMMUNE|MD_TELEPORTBLOCK|MD_DETECTOR|MD_STATUSIMMUNE;
		// Set new damage taken
		setunitdata $@mobid[0],UMOB_DAMAGETAKEN,rand(1,100);
		// Set new drop amount
		setunitdata $@mobid[0],UMOB_DROP_AMOUNT,($orbDropAmount[.@tier] * 2);
		// Set new rank
		setunitdata $@mobid[0],UMOB_DYNAMIC,rand(1,5);
		// Set new hp
		setunitdata $@mobid[0],UMOB_HP,.@hp;
		setunitdata $@mobid[0],UMOB_MAXHP,.@hp;
	}
	freeloop(0);
	
	announce "Event Area ปรากฎแล้ว! (ดูตำแหน่งได้ที่ Minimap)",bc_area,0xA86DBB;
	return;

OnEventAreaDead:
	end;
	
OnGetTier:
	.@floor = getarg(0,1);
	return (.@floor < 10) ? 0 : (.@floor < 20) ? 1 : (.@floor < 30) ? 2 : (.@floor < 50) ? 3 : (.@floor < 75) ? 4 : 5;
	
OnMinute00:
OnMinute10:
OnMinute20:
OnMinute30:
OnMinute40:
OnMinute50:
	freeloop(1);
	.isEvenPvP = (rand(0,1) == 0);
	announce "ภายใน 10 นาทีนี้ Map ที่เป็นเลข" + (.isEvenPvP ? "คู่" : "คี่") + "จะเป็น Map GvG",bc_all,0xFF4040;
	// Loop playing map
	.@playingMapSize = getarraysize($playingMaps$);
	for(.@i = 0; .@i < .@playingMapSize; .@i++){
		if(($playingMaps$[.@i] == "")
				|| ($playingMaps$[.@i] == " "))
		continue;
		
		// Clean Event Monsters
		if(getmapusers($playingMaps$[.@i]) <= 0){
			killmonster $playingMaps$[.@i],"เล่น::OnEventBossDead";
			killmonster $playingMaps$[.@i],"เล่น::OnEventAreaDead";
		}
		
		removemapflag $playingMaps$[.@i],MF_GVG;
		removemapflag $playingMaps$[.@i],MF_GVG_NOPARTY;
		
		// Below 10th floor cannot PVP
		if($playingMapsFloor[.@i] < 10)
		continue;
		
		if(($playingMapsFloor[.@i] % 2) == (.isEvenPvP ? 0 : 1)){
			setmapflag $playingMaps$[.@i],MF_GVG;
			setmapflag $playingMaps$[.@i],MF_GVG_NOPARTY;
		}
	}
	freeloop(0);
	end;
	
OnInit:
	waitingroom "เล่น",0;
	// Maximum floor
	$maximumFloor = 100;
	// Kill amount per floor to spawn big monster {<10<20<30<50<75<100}
	setarray .maximumKillAmount[0],50,100,200,300,400,500;
	// Max spawned amount per floor {<10<20<30<50<75<100}
	setarray .maximumSpawnAmount[0],50,80,120,180,190,200;
	// Available maps
	setarray $maps$[0],"gef_fild00","gef_fild01","gef_fild02","gef_fild03","gef_fild04","gef_fild05","gef_fild06","gef_fild07","gef_fild08","gef_fild09","gef_fild10","gef_fild11","gef_fild12","gef_fild13","gef_fild14","moc_fild01","moc_fild02","moc_fild03","moc_fild07","moc_fild11","moc_fild12","moc_fild13","moc_fild16","moc_fild17","moc_fild18","moc_fild19","pay_fild01","pay_fild02","pay_fild03","pay_fild04","pay_fild05","pay_fild06","pay_fild07","pay_fild08","pay_fild09","pay_fild10","pay_fild11","prt_fild00","prt_fild01","prt_fild02","prt_fild03","prt_fild04","prt_fild05","prt_fild06","prt_fild07","prt_fild08","prt_fild09","prt_fild10","prt_fild11","xmas_fild01","cmd_fild01","cmd_fild02","cmd_fild03","cmd_fild04","cmd_fild05","cmd_fild06","cmd_fild07","cmd_fild08","cmd_fild09","yuno_fild01","yuno_fild02","yuno_fild03","yuno_fild04","ama_fild01","gon_fild01","um_fild01","um_fild02","um_fild03","um_fild04","nif_fild01","nif_fild02","lou_fild01","ayo_fild01","ayo_fild02","yuno_fild05","yuno_fild07","yuno_fild08","yuno_fild09","yuno_fild11","yuno_fild12","ein_fild06","ein_fild07","ein_fild08","ein_fild09","ein_fild10","ein_fild03","ein_fild04","lhz_fild02","lhz_fild03","lhz_fild01","hu_fild07","hu_fild05","hu_fild04","hu_fild01","yuno_fild06","hu_fild02","hu_fild03","hu_fild06","ein_fild01","ein_fild02","ein_fild05","yuno_fild10","ra_fild01","ra_fild02","ra_fild03","ra_fild04","ra_fild05","ra_fild06","ra_fild07","ra_fild08","ra_fild09","ra_fild10","ra_fild11","ra_fild12","ra_fild13","ve_fild01","ve_fild02","ve_fild03","ve_fild04","ve_fild05","ve_fild07","mosk_fild02","moc_fild20","moc_fild21","moc_fild22","man_fild01","man_fild03","spl_fild02","spl_fild03","moc_fild22b","man_fild02","spl_fild01","bra_fild01","dic_fild01","dic_fild02","bif_fild01","bif_fild02","dew_fild01","ma_fild01","ma_fild02","ecl_fild01","prt_fild08a","prt_fild08b","prt_fild08c","prt_fild08d","lasa_fild01","lasa_fild02","tra_fild","gw_fild01","gw_fild02";
	$mapSize = getarraysize($maps$);
	// Random GvG Maps
	.isEvenPvP = (rand(0,1) == 0);
	// Maximum Event Area Monster Amount
	.eventAreaAmount = 10;
	// Maximum Monster HP
	.maximumHp = 2000000000;
	// Special reborn skill chance
	.specialRebornSkillChance = 25;
	deletearray $playingMaps$[0],getarraysize($playingMaps$);
	deletearray $playingMapsFloor[0],getarraysize($playingMapsFloor);
	end;
}