prontera,152,198,3	script	ลากูน	586,{
	mes "คุณมี ^BA17F8" + F_InsertComma(dmPoints) + " Dismantle Points^000000";
	mes "ต้องการบดของที่ไม่ได้ใช้แล้วหรือไม่?";
	mes " ^F81717จะบด Item ทุกชิ้นในกระเป๋าตามประเภทที่เลือก^000000";
	mes " ^3629FFจะไม่บด Item ที่สวมใส่อยู่^000000";
	mes " ^3629FFจะไม่บด Item ที่ Favourite อยู่^000000";
	for(.@i = 0; .@i < .noSellSize; .@i++)
	mes " ^3629FFจะไม่บด " + getitemname(.noSells[.@i]) + "^000000";
	next;
	menu "ไม่ทำอะไร",-
	,"---",-
	,"แลก Dismantle Points เป็น Item แลกเปลี่ยนได้",L_TurnToItem
	,"---",-
	,"บด อาวุธ (ที่มี Random Option) รับ ^BA17F8" + callfunc("F_InsertComma",.weapon) + " Dismantle Points^000000",L_DismantleWeapon
	,"บด อุปกรณ์สวมใส่ (ที่มี Random Option) รับ ^BA17F8" + callfunc("F_InsertComma",.equipment) + " Dismantle Points^000000",L_DismantleEquipment
	,"บด Shadow Gear (ที่มี Random Option) รับ ^BA17F8" + callfunc("F_InsertComma",.shadowGear) + " Dismantle Points^000000",L_DismantleCostume
	,"บด Card รับ ^BA17F8" + callfunc("F_InsertComma",.card) + " Dismantle Points^000000",L_DismantleCard
	,"บด Enchantment รับ ^BA17F8" + callfunc("F_InsertComma",.enchantment) + " Dismantle Points^000000",L_DismantleEnchantment
	,"บด ทุกประเภท ทีเดียวไปเลย",L_DismantleAll
	,"---",-
	,"แลกอาวุธ ใช้ ^BA17F8" + callfunc("F_InsertComma",.weaponPrice) + " Dismantle Points^000000",L_TradeForWeapon
	,"แลกอุปกรณ์สวมใส่ ใช้ ^BA17F8" + callfunc("F_InsertComma",.equipmentPrice) + " Dismantle Points^000000",L_TradeForEquipment
	,"แลก Costume ใช้ ^BA17F8" + callfunc("F_InsertComma",.costumePrice) + " Dismantle Points^000000",L_TradeForCostume
	,"แลก Card ใช้ ^BA17F8" + callfunc("F_InsertComma",.cardPrice) + " Dismantle Points^000000",L_TradeForCard
	,"แลก Enchantment ใช้ ^BA17F8" + callfunc("F_InsertComma",.enchantmentPrice) + " Dismantle Points^000000",L_TradeForEnchantment
	,"แลก Item ที่ต้องการ ใช้ ^BA17F8" + callfunc("F_InsertComma",.selectItemIdPrice) + " Dismantle Points^000000",L_TradeForAnyItem
	;
	close;

	L_TurnToItem:
	.@amount = dmPoints;
	if((countitem(.itemId) + dmPoints) > 30000)
	.@amount = 30000 - countitem(.itemId);

	dmPoints = cap_value(dmPoints - .@amount,0,INT_MAX);
	
	getitem .itemId,.@amount;
	
	close;

	close;
	L_DismantleWeapon:
	.@dismantleType = 1;
	goto L_Dismantle;
	L_DismantleEquipment:
	.@dismantleType = 2;
	goto L_Dismantle;
	L_DismantleCostume:
	.@dismantleType = 3;
	goto L_Dismantle;
	L_DismantleCard:
	.@dismantleType = 4;
	goto L_Dismantle;
	L_DismantleEnchantment:
	.@dismantleType = 5;
	goto L_Dismantle;
	L_DismantleAll:
	.@dismantleType = 0;
	goto L_Dismantle;
	
	L_Dismantle:
	getinventorylist;
	
	freeloop(1);
	for(.@i = @inventorylist_count - 1; .@i >= 0; .@i--){
		// Skip equipped or favourite
		if(@inventorylist_equip[.@i]
				|| @inventorylist_favorite[.@i])
		continue;
		
		// Skip important items
		.@isFoundNoSell = 0;
		for(.@j = 0; .@j < .noSellSize; .@j++)
		{
			if(@inventorylist_id[.@i] == .noSells[.@j])
			{
				.@isFoundNoSell = 1;
				break;
			}	
		}
		if(.@isFoundNoSell)
		continue;
		
		.@itemType = getiteminfo(@inventorylist_id[.@i],ITEMINFO_TYPE);

		.@isDel = 0;
		
		if(.@itemType){
			if((.@itemType == IT_WEAPON)
					&& (@inventorylist_option_id1[.@i] > 0)
					&& ((.@dismantleType == 0) || (.@dismantleType == 1))){
				.@isDel = 1;
				
				.@sumWeapon += .weapon * @inventorylist_amount[.@i];
			}
			else if((.@itemType == IT_ARMOR)
					&& (@inventorylist_option_id1[.@i] > 0)
					&& ((.@dismantleType == 0) || (.@dismantleType == 2))){
				.@isDel = 1;
				
				.@sumEquipment += .equipment * @inventorylist_amount[.@i];
			}
			else if((.@itemType == IT_SHADOWGEAR)
					&& (@inventorylist_option_id1[.@i] > 0)
					&& ((.@dismantleType == 0) || (.@dismantleType == 3))){
				.@isDel = 1;
				
				.@sumShadowGear += .shadowGear * @inventorylist_amount[.@i];
			}
			else if(.@itemType == IT_CARD ){
				.@itemAegisName$ = getiteminfo(@inventorylist_id[.@i],ITEMINFO_AEGISNAME);
				
				if(compare(.@itemAegisName$,"_CARD")
						&& ((.@dismantleType == 0) || (.@dismantleType == 4)))
				{
					.@isDel = 1;
					
					.@sumCard += .card * @inventorylist_amount[.@i];
				}
				else if((.@dismantleType == 0) || (.@dismantleType == 5))
				{
					.@isDel = 1;
					
					.@sumEnchantment += .enchantment * @inventorylist_amount[.@i];
				}
			}
		}
		
		if(.@isDel)
		delitemidx @inventorylist_idx[.@i],@inventorylist_amount[.@i];
	}
	freeloop(0);
	
	dmPoints = cap_value(dmPoints + .@sumWeapon + .@sumEquipment + .@sumShadowGear + .@sumCard + .@sumEnchantment,0,INT_MAX);

	if((.@dismantleType == 0) || (.@dismantleType == 1))
	dispbottom "บด อาวุธ ได้  " + F_InsertComma(.@sumWeapon) + " Dismantle Points";
	if((.@dismantleType == 0) || (.@dismantleType == 2))
	dispbottom "บด อุปกรณ์สวมใส่ ได้ " + F_InsertComma(.@sumEquipment) + " Dismantle Points";
	if((.@dismantleType == 0) || (.@dismantleType == 3))
	dispbottom "บด Shadow Gear ได้ " + F_InsertComma(.@sumShadowGear) + " Dismantle Points";
	if((.@dismantleType == 0) || (.@dismantleType == 4))
	dispbottom "บด Card ได้ " + F_InsertComma(.@sumCard) + " Dismantle Points";
	if((.@dismantleType == 0) || (.@dismantleType == 5))
	dispbottom "บด Enchantment ได้ " + F_InsertComma(.@sumEnchantment) + " Dismantle Points";

	dispbottom "ขณะนี้คุณมี " + F_InsertComma(dmPoints) + " Dismantle Points";
	
	end;
	
	L_TradeForWeapon:
	if(dmPoints >= .weaponPrice){
		dmPoints = cap_value(dmPoints - .weaponPrice,0,INT_MAX);
		GetTheBoxItem(0,1);
		dispbottom "แต้มของคุณขณะนี้ " + F_InsertComma(dmPoints) + " Dismantle Points";
	}
	close;
	L_TradeForEquipment:
	if(dmPoints >= .equipmentPrice){
		dmPoints = cap_value(dmPoints - .equipmentPrice,0,INT_MAX);
		GetTheBoxItem(0,2);
		dispbottom "แต้มของคุณขณะนี้ " + F_InsertComma(dmPoints) + " Dismantle Points";
	}
	close;
	L_TradeForCostume:
	if(dmPoints >= .costumePrice){
		dmPoints = cap_value(dmPoints - .costumePrice,0,INT_MAX);
		GetTheBoxItem(0,3);
		dispbottom "แต้มของคุณขณะนี้ " + F_InsertComma(dmPoints) + " Dismantle Points";
	}
	close;
	L_TradeForCard:
	if(dmPoints >= .cardPrice){
		dmPoints = cap_value(dmPoints - .cardPrice,0,INT_MAX);
		GetTheBoxItem(0,4);
		dispbottom "แต้มของคุณขณะนี้ " + F_InsertComma(dmPoints) + " Dismantle Points";
	}
	close;
	L_TradeForEnchantment:
	if(dmPoints >= .enchantmentPrice){
		dmPoints = cap_value(dmPoints - .enchantmentPrice,0,INT_MAX);
		GetTheBoxItem(0,5);
		dispbottom "แต้มของคุณขณะนี้ " + F_InsertComma(dmPoints) + " Dismantle Points";
	}
	close;
	
	L_TradeForAnyItem:
	mes "ระบุ Item ID ที่ต้องการ";
	input .@i;
	next;
	// Check it was equipment or card
	if((getiteminfo(.@i,ITEMINFO_TYPE) == IT_CARD)
			|| (getiteminfo(.@i,ITEMINFO_TYPE) == IT_SHADOWGEAR)
			|| (getiteminfo(.@i,ITEMINFO_WEAPONLEVEL) > 0)
			|| (getiteminfo(.@i,ITEMINFO_ARMORLEVEL) > 0)){
		mes "คุณมี ^BA17F8" + F_InsertComma(dmPoints) + " Dismantle Points^000000";
		mes "ต้องการแลก ^3F28FF" + getitemname(.@i) + " (" + .@i + ")^000000";
		mes "ด้วย^BA17F8 " + callfunc("F_InsertComma",.selectItemIdPrice) + " Dismantle Points^000000?";
		next;
		menu "แลก",-;
		
		if(dmPoints >= .selectItemIdPrice){
			dmPoints = cap_value(dmPoints - .selectItemIdPrice,0,INT_MAX);
			getitem .@i,1;
		}
		else
		message strcharinfo(0),"ต้องการ " + callfunc("F_InsertComma",.selectItemIdPrice) + " Dismantle Points";
		
		close;
	}
	else{
		mes "ไม่พบ Item ดังกล่าว";
		close;
	}
	
OnInit:
	// Important items
	setarray .noSells,10000000,10000001,10000002,10000003,10000004,10000005,10000006,10000007,10000008;
	.noSellSize = getarraysize(.noSells);
	
	// Points per dismantle
	.weapon = 1;
	.equipment = 1;
	.shadowGear = 1;
	.card = 3;
	.enchantment = 5;
	
	// Price per trade
	.weaponPrice = 95;
	.equipmentPrice = 50;
	.costumePrice = 25;
	.cardPrice = 135;
	.enchantmentPrice = 250;
	
	.selectItemIdPrice = 5555;
	
	.itemId = 10000008;
	
	waitingroom "ย่อยของ",0;
	
	end;
}
