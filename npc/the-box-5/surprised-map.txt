force_3-3	mapflag	noloot
force_3-3	mapflag	noexp
force_3-3	mapflag	novending
force_3-3	mapflag	nochat
force_3-3	mapflag	nowarp
force_3-3	mapflag	nowarpto
force_3-3	mapflag	nosave
force_3-3	mapflag	nobranch
force_3-3	mapflag	notomb

prontera,153,283,3	script	ฮุริ	10460,{
	if((surprisedCooldown - gettimetick(2)) >= 0)
	message strcharinfo(0),"รางวัลสำหรับคุณหมดแล้ว โปรดรอ " + callfunc("GetTimerWording",(surprisedCooldown - gettimetick(2)));
	mes "^0B9552--- [วิธีเล่น] ---^000000";
	mes " เมื่อกำจัด Monster สุ่ม " + callfunc("F_PercentBase10000",$surprisedChance) + " เกิด Monster ตัวใหญ่";
	mes " ";
	mes "^9D42C0--- [รางวัล] ---^000000";
	mes " ^3F28FFItem ประเภทที่อยากได้^000000 (โอกาส " + callfunc("F_PercentBase10000",$surprisedRewardChance) + ") จาก Monster ตัวใหญ่";
	mes " ";
	mes "^C04264--- [โทษภายในห้อง] ---^000000";
	mes " ^F81717Monster จะไม่ให้ EXP, Item";
	mes "เมื่อกำจัด Monster";
	mes " ^F81717สุ่ม " + callfunc("F_PercentBase10000",$surprisedTiredChance) + " เหนื่อย +" + $surprisedTiredValue;
	mes " ^F81717สุ่ม " + callfunc("F_PercentBase10000",$surprisedDebuffsChance) + " ติด Debuff";
	mes " ^F81717สุ่ม " + callfunc("F_PercentBase10000",$surprisedMonsterSpeedChance) + " Monster เคลื่อนที่เร็ว";
	mes " ^F81717สุ่ม " + callfunc("F_PercentBase10000",$surprised50Chance) + " Monster HP 50m";
	mes " ^F81717สุ่ม " + callfunc("F_PercentBase10000",$surprised500Chance) + " Monster HP 500m";
	mes " ^F81717สุ่ม " + callfunc("F_PercentBase10000",$surprisedKillableChance) + " ที่คุณจะตัวใหญ่ และโดนผู้เล่นอื่นโจมตีได้";
	next;
	menu "ตั้งค่าประเภทของที่อยากได้" + (surprisedMapType ? (" ^AF25ACคุณอยากได้ (" + .surprisedTypeName$[surprisedMapType] + ")^000000") : ""),L_Setup
	,"เข้า",L_Enter
	;
	close;
	
	L_Setup:
	mes "เมื่อกำจัด Monster คุณจะมีโอกาส " + callfunc("F_PercentBase10000",$surprisedRewardChance) + " ที่จะได้รับ Item ประเภทที่ต้องการ";
	next;
	
	for(.@i = 0; .@i < getarraysize(.surprisedTypeName$); .@i++)
	.@menu$ += .surprisedTypeName$[.@i]+ ":";

	surprisedMapType = select(.@menu$) - 1;
	mes "คุณอยากได้ " + .surprisedTypeName$[surprisedMapType];
	close;
	
	L_Enter:
	if(!surprisedMapType)
	goto L_Setup;

	SurprisedPlayer();
	warp $surprisedMap$,0,0;
	end;

	L_SpawnMonster:
	.@amount = getarg(0,.monsterAmount);
	
	freeloop(1);
	for(.@i = 0; .@i < .@amount; .@i++){
		.@monsterId = $all_mob_ids[rand($all_mob_id_size)];
		while(callfunc("F_IsDummyMonster",.@monsterId))
		.@monsterId = $all_mob_ids[rand($all_mob_id_size)];
		monster $surprisedMap$,0,0,"--ja--",.@monsterId,1,strnpcinfo(0) + "::OnMonsterDead";
		setunitdata $@mobid[0],UMOB_MODE,MD_CANMOVE|MD_AGGRESSIVE|MD_CANATTACK;
	}
	freeloop(0);

	return;

OnMonsterDead:
	if(!playerattached())
	end;

	SurprisedPlayer();
	
	if(rand(10000) <= $surprisedChance){
		getmapxy(.@tempMap$,.@tempX,.@tempY);
		getfreecell(.@tempMap$,.@cellX,.@cellY,.@tempX,.@tempY,5,5);
		monster .@tempMap$,.@cellX,.@cellY,"--ja--",killedrid,1,strnpcinfo(0) + "::OnSurprisedMonsterKilled",2;
		
		.@newRank = rand(1,5000);
		setunitdata $@mobid[0],UMOB_DYNAMIC,.@newRank;
		setunitdata $@mobid[0],UMOB_DEFLECT,cap_value(.@newRank / 5,0,50);
		setunitdata $@mobid[0],UMOB_MODE,MD_CANMOVE|MD_AGGRESSIVE|MD_CANATTACK;
		
		if(rand(10000) <= $surprisedMonsterSpeedChance)
		setunitdata $@mobid[0],UMOB_SPEED,10;
		
		if(rand(10000) <= $surprised500Chance){
			setunitdata $@mobid[0],UMOB_MAXHP,500000000;
			setunitdata $@mobid[0],UMOB_HP,500000000;
		}
		else if(rand(10000) <= $surprised50Chance){
			setunitdata $@mobid[0],UMOB_MAXHP,50000000;
			setunitdata $@mobid[0],UMOB_HP,50000000;
		}
	}
	
	end;

OnSurprisedMonsterKilled:
	if(!playerattached())
	end;

	if(surprisedRewardCount >= $surprisedRewardPerMap){
		if((surprisedCooldown - gettimetick(2)) >= 0){
			dispbottom "รางวัลสำหรับคุณหมดแล้ว โปรดรอ " + callfunc("GetTimerWording",(surprisedCooldown - gettimetick(2)));
			end;
		}
		else
		surprisedRewardCount = 0;
	}

	.@surprisedRand = rand(10000);
	
	dispbottom "คุณฆ่า Monster วุ่นวาย! ต้องสุ่มได้ <= " + $surprisedRewardChance + " คุณสุ่มได้ " + .@surprisedRand;
	
	if(.@surprisedRand <= $surprisedRewardChance){
		GetTheBoxItem(0,(surprisedMapType + 1));
		surprisedRewardCount++;
		surprisedCooldown = gettimetick(2) + $surprisedCooldown;
		dispbottom "รางวัลใน Map นี้สำหรับคุณเหลือ " + ($surprisedRewardPerMap - surprisedRewardCount) + " รางวัล";
	}
	end;
	
OnTimer60000:
	.@spawnAmount = .monsterAmount - mobcount($surprisedMap$,"");
	if(.@spawnAmount > 0)
	callsub L_SpawnMonster,.@spawnAmount;

	initnpctimer;

	end;

OnInit:
	waitingroom "Map วุ่นวาย",0;

	$surprisedMap$ = "force_3-3";
	$surprisedCooldown = 600;

	.monsterAmount = 100;

	setarray .surprisedTypeName$[0],"อาวุธ","อุปกรณ์สวมใส่","Costume","การ์ด","Enchantment";
	
	$surprisedTiredChance = 500;			// Chance for player to get tired
	$surprisedTiredValue = 1;				// How much tired
	$surprisedDebuffsChance = 500;			// Non freeze debuffs to player chance
	$surprisedDebuffChance = 500;			// Freeze debuff{s} to player chance
	$surprisedKillableChance = 500;			// Player can be hit by another player chance	

	$surprisedChance = 5000;				// Chance to surprised!
	$surprisedMonsterSpeedChance = 2500;	// Monster ultra speed chance
	$surprised500Chance = 500;				// Monster 500m HP chance
	$surprised50Chance = 1500;				// Monster 50m HP chance
	
	$surprisedRewardChance = 5000;			// Chance to get reward
	$surprisedRewardPerMap = 5;				// Reward per map
	
	callsub L_SpawnMonster;
	
	initnpctimer;
	
	end;
}
