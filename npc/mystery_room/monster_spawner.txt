bat_c01,97,86,5	script	เริ่มต่อสู้	464,{
	// Unexpected error
	if($mysterySpawner == 2){
		message strcharinfo(0),"Monster ตัวเก่ายังอยู่ โปรดฆ่ามันเสียก่อน";
		end;
	}
	// Faster countdown
	else if($mysterySpawner == 1){
		callsub L_AlreadyTapCheck;
		end;
	}
	else{
		message strcharinfo(0),"ระดับขณะนี้: " + callfunc("F_InsertComma",$mysteryLevel);
		menu "เริ่มต่อสู้",-;
		// Unexpected error
		if($mysterySpawner != 0){
			callsub L_AlreadyTapCheck;
			end;
		}
		else{
			// Announcing
			$mysterySpawner = 1;
			// Reset countdown
			$mysterySpawnerCountdown = 10;
			// Reset dead count
			$mysteryDeadCount = 0;
			// Reset tap users
			deletearray $mysteryTap[0],getarraysize($mysteryTap);
			// Set tap user level
			$mysteryTapUserLv = BaseLevel;
			// Every 10 level had 50% chance to spawn MvP (Double reward at the end)
			if($mysteryLevel % 10 == 0 && rand (0,100) <= 50)
			$mysteryBoss = 1;
			// Otherwise not MvP
			else
			$mysteryBoss = 0;
			// Set monster tier
			.@sumLevel = $mysteryLevel;
			freeloop(1);
			while(.@sumLevel > 600)
				.@sumLevel -= 600;
			freeloop(0);
			$monsterTier = (.@sumLevel + 30) / 30 ;
			if($monsterTier <= 1)
			$monsterTier = 1;
			else if($monsterTier >= 19)
			$monsterTier = 19;
			// Set game mode
			$mysteryMode = rand(0,100);
			// Set monster buff
			$mysteryBuff = rand(0,100);
			// +1 Current Level
			$mysteryLevel++;
			// Announce
			delwaitingroom;
			if($mysteryBoss)
			.@sumInformation$ = "Mode: MvP!!";
			else if($mysteryMode <= 19) // 20%
			{
				.@sumInformation$ = "Mode: ปกติ (Lv. " + $mysteryLevel + ")";
				$mysteryMode = 0;
			}
			else if($mysteryMode <= 69) // 50%
			{
				.@sumInformation$ = "Mode: ตามเลเวลผู้กด (Lv. " + $mysteryTapUserLv + ")";
				$mysteryMode = 1;
			}
			else if($mysteryMode <= 84) // 15%
			{
				.@sumInformation$ = "Mode: ปกติมหาศาล (Lv. " + $mysteryLevel + ")";
				$mysteryMode = 2;
			}
			else if($mysteryMode <= 100) // 15%
			{
				.@sumInformation$ = "Mode: ตามเลเวลผู้กดมหาศาล (Lv. " + $mysteryTapUserLv + ")";
				$mysteryMode = 3;
			}
			waitingroom .@sumInformation$,0;
			mapannounce "bat_c01",.@sumInformation$,0,0x93FF28;
			// Start countdown
			initnpctimer;
		}
		end;
	}
	
	// Countdown
OnTimer1000:
	$mysterySpawnerCountdown--;
	// Timeout
	if($mysterySpawnerCountdown <= 0){
		// Hide npc
		hideonnpc "กล่องปริศนา#1";
		hideonnpc "กล่องปริศนา#2";
		hideonnpc "กล่องปริศนา#3";
		hideonnpc "กล่องปริศนา#4";
		hideonnpc "เริ่มต่อสู้";
		// Set monster alive
		$mysterySpawner = 2;
		// Set countdown to 0
		$mysterySpawnerCountdown = 0;
		// Set mystery box reward
		if(rand(0,1) == 0)
		$mysteryBox1ItemId = $allWeaponId[rand(0,$allWeaponIdArraySize - 1)];
		else
		$mysteryBox1ItemId = $allArmorId[rand(0,$allArmorIdArraySize - 1)];
		if(rand(0,1) == 0)
		$mysteryBox2ItemId = $allWeaponId[rand(0,$allWeaponIdArraySize - 1)];
		else
		$mysteryBox2ItemId = $allArmorId[rand(0,$allArmorIdArraySize - 1)];
		if(rand(0,1) == 0)
		$mysteryBox3ItemId = $allWeaponId[rand(0,$allWeaponIdArraySize - 1)];
		else
		$mysteryBox3ItemId = $allArmorId[rand(0,$allArmorIdArraySize - 1)];
		if(rand(0,1) == 0)
		$mysteryBox4ItemId = $allWeaponId[rand(0,$allWeaponIdArraySize - 1)];
		else
		$mysteryBox4ItemId = $allArmorId[rand(0,$allArmorIdArraySize - 1)];
		// Monster set
		if($mysteryMode == 0 || $mysteryMode == 2)
		.@sumMonsterSet = $monsterTier;
		else if($mysteryMode == 1 || $mysteryMode == 3)
		{
			.@sumMonsterSet = $mysteryTapUserLv / 10;
			if(.@sumMonsterSet <= 1)
			.@sumMonsterSet = 1;
		}
		// Force to spawn boss
		if($mysteryBoss)
		$mysteryMonsterId = $allMvp[rand(0,getarraysize($allMvp)-1)];
		// Set monster id (Change monster set every 30 level)
		else
		$mysteryMonsterId = getd("$mr" + .@sumMonsterSet + "id[" + rand(0,getarraysize(getd("$mr" + .@sumMonsterSet + "id")) - 1) + "]");
		// Monster count
		.@sumLevel = $mysteryLevel;
		if($mysteryMode == 1 || $mysteryMode == 3)
		.@sumLevel = $mysteryTapUserLv;
		.@sumSpawnCount = rand(1,.@sumLevel);
		.@sumSpawnCount /= 5;
		if(.@sumSpawnCount <= 1)
		.@sumSpawnCount = 1;
		// Crazy spawn mode
		if($mysteryMode == 2 || $mysteryMode == 3)
		.@sumSpawnCount = 40;
		// MvP should be 1~15
		if($mysteryBoss)
		.@sumSpawnCount = rand(1,15);
		// Cap at 40
		if(.@sumSpawnCount >= 40)
		.@sumSpawnCount = 40;
		$mysteryRoomSpawnCount = .@sumSpawnCount;
		// Spawn now
		callsub L_SpawnMonster;
		// Announce
		mapannounce "bat_c01",getmonsterinfo($mysteryMonsterId,MOB_NAME) + " (" + $mysteryRoomSpawnCount + " ตัว) ได้ปรากฎขึ้นมาแล้ว!!",0,0xFF5733;
		// Stop countdown
		stopnpctimer;
	}
	// Information for players
	else{
		// Announce
		mapannounce "bat_c01","เตรียมตัว.. Monster จะมาภายใน " + $mysterySpawnerCountdown + " วินาที!!",0,0xFF5733;
		// Continue countdown
		initnpctimer;
	}
	end;
	
	// Player tap to reduce spawn countdown
	L_AlreadyTapCheck:
	// Already tap
	for(.@i = 0; .@i < getarraysize($mysteryTap); .@i++){
		if(getcharid(3) == $mysteryTap[.@i]){
			message strcharinfo(0),"คุณกดเร่งเวลาไปแล้ว";
			end;
		}
	}
	
	// Check still had timer to reduce
	if($mysterySpawnerCountdown > 0){
		// Reduce countdown to spawn
		$mysterySpawnerCountdown -= 4;
		// Set tap users
		setarray $mysteryTap[getarraysize($mysteryTap)],getcharid(3);
	}
	return;
	
	L_SpawnMonster:
	// Reset kill count
	$mysteryRoomKilled = 0;
	// Loop spawn
	freeloop(1);
	for(.@i = 0; .@i < $mysteryRoomSpawnCount; .@i++){
		monster "bat_c01",rand(96,110),rand(96,110),"--ja--",$mysteryMonsterId,1,strnpcinfo(0) + "::OnMonsterDead";
		
		getunitdata $@mobid[0],.@tempData;
		
		/*npctalk "HP: " + .@tempData[2];
	npctalk "Max HP: " + .@tempData[3];
	npctalk "Speed: " + .@tempData[8];
	npctalk "Str: " + .@tempData[24];
	npctalk "Agi: " + .@tempData[25];
	npctalk "Vit: " + .@tempData[26];
	npctalk "Int: " + .@tempData[27];
	npctalk "Dex: " + .@tempData[28];
	npctalk "Luk: " + .@tempData[29];
	npctalk "Atk Range: " + .@tempData[32];
	npctalk "Atk Min: " + .@tempData[33];
	npctalk "Atk Max: " + .@tempData[34];
	npctalk "MAtk Min: " + .@tempData[35];
	npctalk "MAtk Max: " + .@tempData[36];
	npctalk "Def: " + .@tempData[37];
	npctalk "MDef: " + .@tempData[38];
	npctalk "Hit: " + .@tempData[39];
	npctalk "Flee: " + .@tempData[40];
	npctalk "P.Dodge: " + .@tempData[41];
	npctalk "Cri: " + .@tempData[42];
	
	npctalk "---";*/
		
		.@tempHp = .@tempData[2];
		.@tempMaxHP = .@tempData[3];
		.@tempSpeed = .@tempData[8];
		.@tempStr = .@tempData[24];
		.@tempAgi = .@tempData[25];
		.@tempVit = .@tempData[26];
		.@tempInt = .@tempData[27];
		.@tempDex = .@tempData[28];
		.@tempLuk = .@tempData[29];
		.@tempAtkRange = .@tempData[32];
		.@tempAtkMin = .@tempData[33];
		.@tempAtkMax = .@tempData[34];
		.@tempMAtkMin = .@tempData[35];
		.@tempMAtkMax = .@tempData[36];
		.@tempDef = .@tempData[37];
		.@tempMDef = .@tempData[38];
		.@tempHit = .@tempData[39];
		.@tempFlee = .@tempData[40];
		.@tempPDodge = .@tempData[41];
		.@tempCri = .@tempData[42];
		
		// x2 Every 600 Level
		.@multiplier = ($mysteryLevel / 600) + 1;

		if(.@multiplier <= 1)
		.@multiplier = 1;
		
		.@multiplier2 = .@multiplier / 2;
		if(.@multiplier2 <= 1)
		.@multiplier2 = 1;

		setunitdata $@mobid[0],UMOB_HP,.@tempHp * .@multiplier;
		setunitdata $@mobid[0],UMOB_MAXHP,.@tempMaxHP * .@multiplier;
		setunitdata $@mobid[0],UMOB_SPEED,.@tempSpeed * .@multiplier;
		setunitdata $@mobid[0],UMOB_MODE,(0x0000001 | 0x0000004 | 0x0000080);
		setunitdata $@mobid[0],UMOB_STR,.@tempStr * .@multiplier;
		setunitdata $@mobid[0],UMOB_AGI,.@tempAgi * .@multiplier;
		setunitdata $@mobid[0],UMOB_VIT,.@tempVit * .@multiplier;
		setunitdata $@mobid[0],UMOB_INT,.@tempInt * .@multiplier;
		setunitdata $@mobid[0],UMOB_DEX,.@tempDex * .@multiplier;
		setunitdata $@mobid[0],UMOB_LUK,.@tempLuk * .@multiplier;
		setunitdata $@mobid[0],UMOB_ATKRANGE,.@tempAtkRange * .@multiplier;
		setunitdata $@mobid[0],UMOB_ATKMIN,.@tempAtkMin * .@multiplier;
		setunitdata $@mobid[0],UMOB_ATKMAX,.@tempAtkMax * .@multiplier;
		setunitdata $@mobid[0],UMOB_MATKMIN,.@tempMAtkMin * .@multiplier;
		setunitdata $@mobid[0],UMOB_MATKMAX,.@tempMAtkMax * .@multiplier;
		setunitdata $@mobid[0],UMOB_DEF,.@tempDef * .@multiplier;
		setunitdata $@mobid[0],UMOB_MDEF,.@tempMDef * .@multiplier;
		setunitdata $@mobid[0],UMOB_HIT,.@tempHit * .@multiplier;
		setunitdata $@mobid[0],UMOB_FLEE,.@tempFlee * .@multiplier2;
		setunitdata $@mobid[0],UMOB_PDODGE,.@tempPDodge * .@multiplier;
		setunitdata $@mobid[0],UMOB_CRIT,.@tempCri * .@multiplier;
		
		/*getunitdata $@mobid[0],.@tempData;
	
	npctalk "HP: " + .@tempData[2];
	npctalk "Max HP: " + .@tempData[3];
	npctalk "Speed: " + .@tempData[8];
	npctalk "Str: " + .@tempData[24];
	npctalk "Agi: " + .@tempData[25];
	npctalk "Vit: " + .@tempData[26];
	npctalk "Int: " + .@tempData[27];
	npctalk "Dex: " + .@tempData[28];
	npctalk "Luk: " + .@tempData[29];
	npctalk "Atk Range: " + .@tempData[32];
	npctalk "Atk Min: " + .@tempData[33];
	npctalk "Atk Max: " + .@tempData[34];
	npctalk "MAtk Min: " + .@tempData[35];
	npctalk "MAtk Max: " + .@tempData[36];
	npctalk "Def: " + .@tempData[37];
	npctalk "MDef: " + .@tempData[38];
	npctalk "Hit: " + .@tempData[39];
	npctalk "Flee: " + .@tempData[40];
	npctalk "P.Dodge: " + .@tempData[41];
	npctalk "Cri: " + .@tempData[42];*/
	}
	freeloop(0);
	return;
	
	// Any monster dead goes here
OnMonsterDead:
	$mysteryRoomKilled++;
	if($mysteryRoomKilled < $mysteryRoomSpawnCount)
	{
		// Announce
		mapannounce "bat_c01","ฆ่าแล้ว " + $mysteryRoomKilled + "/" + $mysteryRoomSpawnCount,0;
		end;
	}
	// All monster dead
	$mysterySpawner = 0;
	// Sum EXP = Base EXP * Level
	.sumEXP = .baseEXP * $mysteryLevel;
	if($mysteryMode == 1 || $mysteryMode == 3)
	.sumEXP = .baseEXP * $mysteryTapUserLv;
	// Dynamic EXP ( 0 ~ (Sum EXP / 2))
	.@dynamicEXP = rand(0,(.sumEXP / 2));
	// Unexpected error
	if(.@dynamicEXP <= 0)
	.@dynamicEXP = 0;
	// No death bonus
	if($mysteryDeadCount <= 0){
		// Dynamic EXP ( 0 ~ (EXP / 5))
		.@noDeathEXP = rand(0,(.sumEXP / 5));
		// Unexpected error
		if(.@noDeathEXP <= 0)
		.@noDeathEXP = 0;
	}
	.sumEXP += .@dynamicEXP + .@noDeathEXP;
	// Boss bonus
	if($mysteryBoss)
	.sumEXP *= 2;
	// Show npc
	hideoffnpc "เริ่มต่อสู้";
	hideoffnpc "กล่องปริศนา#1";
	hideoffnpc "กล่องปริศนา#2";
	hideoffnpc "กล่องปริศนา#3";
	hideoffnpc "กล่องปริศนา#4";
	// Announce
	mapannounce "bat_c01",getmonsterinfo($mysteryMonsterId,MOB_NAME) + " ถูกฆ่าหมดแล้ว!! ทุกคนภายใน Map ได้รับ " + callfunc("F_InsertComma",.sumEXP) + " EXP " + callfunc("F_InsertComma",.sumEXP/2) + " Job EXP",0;
	// Give EXP to all player in map
	addrid(5,0,"bat_c01");
	// EXP reward
	getexp .sumEXP,.sumEXP/2;
	// Zeny reward
	if($mysteryMode == 1 || $mysteryMode == 3)
	Zeny += $mysteryTapUserLv * 40;
	else
	Zeny += $mysteryLevel * 40;
	// Visual effect
	specialeffect2 399;
	// Reset open box count
	isOpenMysteryBox = 0;
	end;
	
	// Initialize
OnInit:
	// Base EXP
	.baseEXP = 10000;
	// Respawn monster if still in progress
	if($mysterySpawner == 2)
	callsub L_SpawnMonster;
	// Otherwise reset it
	else
	$mysterySpawner = 0;
	// Hide / Show here
	if($mysterySpawner <= 0)
	hideoffnpc "เริ่มต่อสู้";
	else
	hideonnpc "เริ่มต่อสู้";
	waitingroom "เริ่มต่อสู้",0;
	end;
}
