-	script	NPCKillEvent	-1,{
	end;
	
OnNPCKillEvent:
	if(!IsTownMap()
			&& (strcharinfo(3) != "06guild_01")
			&& (strcharinfo(3) != "jor_sanct")
			&& playerattached()){
		if(@monsterDeadCooldown
				&& (@monsterDeadCooldown > gettimetick(2)))
		.@isSkipGetUnitData = 1;
		
		if(!.@isSkipGetUnitData){
			getunitdata(killedgid,@deadMonster);
			
			@monsterDeadCooldown = gettimetick(2) + 3;
		}
		
		if(@deadMonster[UMOB_MASTERAID] > 0)
		end;
		
		// Macro Detector
		if(!isMacroDetecting){
			ka++;
			if((mdt + ka) >= max(bmDetectAmount,.killAmountMacro)){
				isMacroDetecting = 1;
				bmDetectAmount = rand(100,300);
				addtimer 60000,strnpcinfo(3) + "::OnCheckBotMacro";
				bmItemId = .bmItemIds[rand(getarraysize(.bmItemIds))];
				bmItemAmount = rand(1,99);
				#CASHPOINTS = 100;
				announce "โปรดซื้อ " + getitemname(bmItemId) + " " + bmItemAmount + " ชิ้น ใน Cash Shop ภายใน 60 วินาที",bc_self,0xFF6D6D,FW_BOLD,43;
				mdt = 0;
				ka = 0;
				setpcblock PCBLOCK_ATTACK,true;
				setpcblock PCBLOCK_SKILL,true;
				setpcblock PCBLOCK_USEITEM,true;
				setpcblock PCBLOCK_IMMUNE,true;
				setpcblock PCBLOCK_COMMANDS,true;
				duplicate_dynamic("ซื้อของแล้วกดได้เลย#SELF");
			}
		}
		
		.@monsterLevel = @deadMonster[UMOB_LEVEL];
		.@monsterRank = @deadMonster[UMOB_DYNAMIC];
		
		// Trader
		// Random chance to spawn (If not spawn yet)
		.@isTraderHad = getd("$@tit" + strcharinfo(3));
		.@isTraderFull = getd("$@ttf" + strcharinfo(3));
		if((!.@isTraderHad || .@isTraderFull)
				&& (rand(10000) <= .traderSpawnChance)){
			// Get monster drops
			getmobdrops(killedrid);
			
			// Store random drop item
			.@monsterDropItemAmount = $@MobDrop_count;
			copyarray .@monsterDropItems[0],$@MobDrop_item[0],.@monsterDropItemAmount;
			copyarray .@monsterDropitemRates[0],$@MobDrop_rate[0],.@monsterDropItemAmount;
			
			// Random index
			.@randomDropIndex = rand(.@monsterDropItemAmount);
			
			// Getting item id
			.@monsterDropItemId = .@monsterDropItems[.@randomDropIndex];
			// Getting item type
			.@type = getiteminfo(.@monsterDropItemId,ITEMINFO_TYPE);
			
			// Do not use equipment as requirement item id
			.@retry = .@monsterDropItemAmount;
			while((.@retry > 0)
			&& ((.@type == 4) || (.@type == 5) || (.@type == 12))){
				.@retry--;
				
				// Random index
				.@randomDropIndex = rand(.@monsterDropItemAmount);
				// Getting item id
				.@monsterDropItemId = .@monsterDropItems[.@randomDropIndex];
				// Getting item type
				.@type = getiteminfo(.@monsterDropItemId,ITEMINFO_TYPE);
			}
			
			.@monsterDropItemRate = cap_value(.@monsterDropitemRates[.@randomDropIndex],1,10000);
			
			if(!IsSellInShop(.@monsterDropItemId)){
				set getd("$@tii" + strcharinfo(3)),.@monsterDropItemId;
				set getd("$@tia" + strcharinfo(3)),.@monsterDropItemRate;
				set getd("$@tta" + strcharinfo(3)),0;
				set getd("$@tml" + strcharinfo(3)),.@monsterLevel;
				set getd("$@tmi" + strcharinfo(3)),killedrid;
				set getd("$@tli" + strcharinfo(3)),gettimetick(2);
				// Restart trading
				if(getd("$@ttf" + strcharinfo(3))){
					set getd("$@ttf" + strcharinfo(3)),0;
					announce "พ่อค้าหน้าเลือด (THE BOX) ได้ Reset รางวัลแล้ว!",bc_map,0xFF6D6D;
				}
				else{
					// Set as spawned (Disappear when reload scripts)
					set getd("$@tit" + strcharinfo(3)),1;
					// Random position in current map
					getfreecell strcharinfo(3),.@x,.@y;
					set getd("$@tX" + strcharinfo(3)),.@x;
					set getd("$@tY" + strcharinfo(3)),.@y;
					// Spawn now
					duplicate "พ่อค้าหน้าเลือด#THE_BOX",strcharinfo(3),.@x,.@y;
					// Mark on minimap
					viewpointmap strcharinfo(3),1,.@x,.@y,1,0xFF6D6D;
					announce "พ่อค้าหน้าเลือด (THE BOX) ได้ปรากฎใน Map แล้ว!",bc_map,0xFF6D6D;
				}
			}
		}
		
		if(.@monsterLevel >= BaseLevel)
		latestCombo = .@monsterRank;
		else
		latestCombo = 0;
		
		// Map Limitation
		.@lastFoundMapAmount = getarraysize(lastFoundTheBoxMap$);
		.@currentMapLimitation = 0;
		if(.@lastFoundMapAmount > 0){
			for(.@i = 0; .@i < .@lastFoundMapAmount; .@i++){
				if(lastFoundTheBoxMap$[.@i] == strcharinfo(3)){
					.@isFoundSameMap = 1;
					
					.@currentMapLimitation = mapLimitation[.@i];
					
					break;
				}
			}
		}
		
		// Chance
		.@monsterLevelBonus = cap_value(
		(.@monsterLevel / .monsterLevelDivider) * 100
		,1
		,INT_MAX);
		
		.@mapLimitation = cap_value(
		.@currentMapLimitation * .staminaDecrease
		,0
		,.baseChance);
		
		.@baseCalculated = (.baseChance * .@monsterRank);
		
		.@calculated = (.@baseCalculated - .@mapLimitation);
		
		.@chance = ((.baseChance - .@mapLimitation) > 0)
		? (.@calculated + .@monsterLevelBonus)
		: 0;
		
		if(.@chance <= 0)
		end;
		
		.@rand = rand(10000);
		
		if(#isShowTheBoxRate)
		dispbottom "ต้องสุ่มได้น้อยกว่าหรือเท่ากับ " + .@chance + " คุณสุ่มได้ " + .@rand;
		
		if(.@rand <= .@chance){
			if(!.@isFoundSameMap){
				// Remove first map if amount > Maximum
				if(.@lastFoundMapAmount >= .sameMapAmount){
					deletearray lastFoundTheBoxMap$[0],1;
					deletearray mapLimitation[0],1;
				}
			}
			
			isReadyToClaimed = 1;
			
			// Show now
			duplicate_dynamic("THE BOX#SELF");
			
			specialeffect2 747;
		}
	}

	end;
	
	
OnCheckBotMacro:
	if(isMacroDetecting){
		isMacroDetecting = 0;
		
		#CASHPOINTS = 0;
		
		setpcblock PCBLOCK_ATTACK,false;
		setpcblock PCBLOCK_SKILL,false;
		setpcblock PCBLOCK_USEITEM,false;
		setpcblock PCBLOCK_IMMUNE,false;
		setpcblock PCBLOCK_COMMANDS,false;
		
		if(countitem(bmItemId) != bmItemAmount){
			tired = 100;
			RefreshDebuff();
			announce "คุณไม่ยอมซื้อ " + getitemname(bmItemId) + " จาก Cash Shop",bc_self,0xFF6D6D,FW_BOLD,43;
		}
		else{
			.@bmReward = rand(5,15);
			dmPoints = cap_value(dmPoints + .@bmReward,0,INT_MAX);
			announce "ขอบคุณมาก! คุณได้รับ " + .@bmReward + " Dismantle Points",bc_self,0xFF6D6D,FW_BOLD,43;
		}
		delitem bmItemId,countitem(bmItemId);
	}
	end;
	
OnInit:
	.killAmountMacro = 100;		// How many killed to start for detecting macro
	setarray .bmItemIds[0],7492,7491,7490,7489,7488,7487,7486,7485;
	
	.traderSpawnChance = 100;	// 10000 = 100%
	.baseChance = 100;			// 10000 = 100%
	.staminaDecrease = 20;		// 10000 = 100%
	.monsterLevelDivider = 10;	// Use to increase chance to found THE BOX (Monster Level / n)
	.sameMapAmount = 3;
	$maximumMapLimitation = 5;
	
	addrid(0);
	if(isMacroDetecting)
	isMacroDetecting = 0;
	end;
}
