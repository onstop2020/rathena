-	script	NPCKillEvent	-1,{
	end;
	
OnSurprisedMonsterKilled:
	if(surprisedRewardCount >= $surprisedRewardPerMap){
		if((surprisedCooldown - gettimetick(2)) >= 0){
			dispbottom "รางวัลสำหรับคุณหมดแล้ว โปรดรอ " + callfunc("GetTimerWording",(surprisedCooldown - gettimetick(2)));
			end;
		}
		else
		surprisedRewardCount = 0;
	}

	.@surprisedRand = rand(10000);
	
	dispbottom "คุณฆ่า Monster วุ่นวาย! ต้องสุ่มได้ <= " + $surprisedRewardChance + " คุณสุ่มได้ " + .@surprisedRand;
	
	if(.@surprisedRand <= $surprisedRewardChance){
		GetTheBoxItem(0,(surprisedMapType + 1));
		surprisedRewardCount++;
		surprisedCooldown = gettimetick(2) + $surprisedCooldown;
		dispbottom "รางวัลใน Map นี้สำหรับคุณเหลือ " + ($surprisedRewardPerMap - surprisedRewardCount) + " รางวัล";
	}
	end;
	
OnNPCKillEvent:
	if(!IsTownMap()
			&& (strcharinfo(3) != "06guild_01")
			&& playerattached()){
		getunitdata(killedgid,.@deadMonster);
		
		if(.@deadMonster[UMOB_MASTERAID] > 0)
		end;
		
		// Macro Detector
		macroDetector = cap_value(macroDetector + 1,0,.killAmountMacro);
		if((macroDetector >= .killAmountMacro)
				&& !isMacroDetecting){
			isMacroDetecting = 1;
			specialeffect2 644;
			setpcblock PCBLOCK_ATTACK,true;
			setpcblock PCBLOCK_SKILL,true;
			setpcblock PCBLOCK_IMMUNE,true;
			addtimer 3000,strnpcinfo(3) + "::OnMacroStart";
			announce "อีก 3 วินาทีจะทำการตรวจ Macro..",bc_self,0xFF6D6D,FW_BOLD,43;
		}
		
		// Surprised Map
		if(strcharinfo(3) == $surprisedMap$){
			SurprisedPlayer();
			
			if(rand(10000) <= $surprisedChance){
				getmapxy(.@tempMap$,.@tempX,.@tempY);
				getfreecell(.@tempMap$,.@cellX,.@cellY,.@tempX,.@tempY,5,5);
				monster .@tempMap$,.@cellX,.@cellY,"--ja--",killedrid,1,strnpcinfo(0) + "::OnSurprisedMonsterKilled",2;
				
				setunitdata $@mobid[0],UMOB_DYNAMIC,rand(1,5000);
				setunitdata $@mobid[0],UMOB_MODE,MD_CANMOVE|MD_AGGRESSIVE|MD_CANATTACK;
				
				if(rand(10000) <= $surprisedMonsterSpeedChance)
				setunitdata $@mobid[0],UMOB_SPEED,10;
				
				if(rand(10000) <= $surprised500Chance){
					setunitdata $@mobid[0],UMOB_MAXHP,500000000;
					setunitdata $@mobid[0],UMOB_HP,500000000;
				}
				else if(rand(10000) <= $surprised50Chance){
					setunitdata $@mobid[0],UMOB_MAXHP,50000000;
					setunitdata $@mobid[0],UMOB_HP,50000000;
				}
			}
			
			end;
		}
		
		.@monsterLevel = .@deadMonster[UMOB_LEVEL];
		.@monsterRank = .@deadMonster[UMOB_DYNAMIC];
		
		// Trader
		// Random chance to spawn (If not spawn yet)
		.@isTraderHad = getd("$@tit" + strcharinfo(3));
		.@isTraderFull = getd("$@ttf" + strcharinfo(3));
		if((!.@isTraderHad || .@isTraderFull)
				&& (rand(10000) <= .traderSpawnChance)){
			// Get monster drops
			getmobdrops(killedrid);
			
			// Store random drop item
			.@monsterDropItemAmount = $@MobDrop_count;
			copyarray .@monsterDropItems[0],$@MobDrop_item[0],.@monsterDropItemAmount;
			copyarray .@monsterDropitemRates[0],$@MobDrop_rate[0],.@monsterDropItemAmount;
			
			// Random index
			.@randomDropIndex = rand(.@monsterDropItemAmount);
			
			// Getting item id
			.@monsterDropItemId = .@monsterDropItems[.@randomDropIndex];
			// Getting item type
			.@type = getiteminfo(.@monsterDropItemId,ITEMINFO_TYPE);
			
			// Do not use equipment as requirement item id
			.@retry = .@monsterDropItemAmount;
			while((.@retry > 0)
			&& ((.@type == 4) || (.@type == 5) || (.@type == 12))){
				.@retry--;
				
				// Random index
				.@randomDropIndex = rand(.@monsterDropItemAmount);
				// Getting item id
				.@monsterDropItemId = .@monsterDropItems[.@randomDropIndex];
				// Getting item type
				.@type = getiteminfo(.@monsterDropItemId,ITEMINFO_TYPE);
			}
			
			.@monsterDropItemRate = cap_value(.@monsterDropitemRates[.@randomDropIndex],1,10000);
			
			if(!IsSellInShop(.@monsterDropItemId)){
				set getd("$@tii" + strcharinfo(3)),.@monsterDropItemId;
				set getd("$@tia" + strcharinfo(3)),.@monsterDropItemRate;
				set getd("$@tta" + strcharinfo(3)),0;
				set getd("$@tml" + strcharinfo(3)),.@monsterLevel;
				set getd("$@tmi" + strcharinfo(3)),killedrid;
				set getd("$@tli" + strcharinfo(3)),gettimetick(2);
				// Restart trading
				if(getd("$@ttf" + strcharinfo(3))){
					set getd("$@ttf" + strcharinfo(3)),0;
					announce "พ่อค้าหน้าเลือด (THE BOX) ได้ Reset รางวัลแล้ว!",bc_map,0xFF6D6D;
				}
				else{
					// Set as spawned (Disappear when reload scripts)
					set getd("$@tit" + strcharinfo(3)),1;
					// Random position in current map
					getfreecell strcharinfo(3),.@x,.@y;
					set getd("$@tX" + strcharinfo(3)),.@x;
					set getd("$@tY" + strcharinfo(3)),.@y;
					// Spawn now
					duplicate "พ่อค้าหน้าเลือด#THE_BOX",strcharinfo(3),.@x,.@y;
					// Mark on minimap
					viewpointmap strcharinfo(3),1,.@x,.@y,1,0xFF6D6D;
					announce "พ่อค้าหน้าเลือด (THE BOX) ได้ปรากฎใน Map แล้ว!",bc_map,0xFF6D6D;
				}
			}
		}
		
		if(.@monsterLevel >= BaseLevel)
		latestCombo = .@monsterRank;
		else
		latestCombo = 0;
		
		// Map Limitation
		.@lastFoundMapAmount = getarraysize(lastFoundTheBoxMap$);
		.@currentMapLimitation = 0;
		if(.@lastFoundMapAmount > 0){
			for(.@i = 0; .@i < .@lastFoundMapAmount; .@i++){
				if(lastFoundTheBoxMap$[.@i] == strcharinfo(3)){
					.@isFoundSameMap = 1;
					
					.@currentMapLimitation = mapLimitation[.@i];
					
					break;
				}
			}
		}
		
		// Chance
		.@monsterLevelBonus = cap_value(
		(.@monsterLevel / .monsterLevelDivider) * 100
		,1
		,INT_MAX);
		
		.@mapLimitation = cap_value(
		.@currentMapLimitation * .staminaDecrease
		,0
		,.baseChance);
		
		.@baseCalculated = (.baseChance * .@monsterRank);
		
		.@calculated = (.@baseCalculated - .@mapLimitation);
		
		.@chance = ((.baseChance - .@mapLimitation) > 0)
		? (.@calculated + .@monsterLevelBonus)
		: 0;
		
		/*dispbottom "Base: "
		+ .@baseCalculated
		+ " | Penalty: "
		+ (.@currentMapLimitation * .staminaDecrease)
		+ " | Calculated: "
		+ .@calculated
		+ " | Monster Bonus: "
		+ .@monsterLevelBonus
		+ " | Finalize: " + .@chance;*/
		
		if(.@chance <= 0)
		end;
		
		.@rand = rand(10000);
		
		if(#isShowTheBoxRate)
		dispbottom "ต้องสุ่มได้น้อยกว่าหรือเท่ากับ " + .@chance + " คุณสุ่มได้ " + .@rand;
		
		if(.@rand <= .@chance){
			if(!.@isFoundSameMap){
				// Remove first map if amount > Maximum
				if(.@lastFoundMapAmount >= .sameMapAmount){
					deletearray lastFoundTheBoxMap$[0],1;
					deletearray mapLimitation[0],1;
				}
			}
			
			isReadyToClaimed = 1;
			
			// Show now
			duplicate_dynamic("THE BOX#SELF");
			
			specialeffect2 747;
		}
	}

	end;
	
OnMacroStart:
	setpcblock PCBLOCK_ATTACK,false;
	setpcblock PCBLOCK_SKILL,false;
	setpcblock PCBLOCK_IMMUNE,false;
	
	macroIdleTimer = checkidle();
	macroIdleTimerTarget = 1000 * rand(1,10);
	atcommand "@monsterignore";
	addtimer macroIdleTimerTarget,strnpcinfo(3) + "::OnMacro";
	announce "โปรดอยู่นิ่ง ๆ " + (macroIdleTimerTarget / 1000) + " วินาที..",bc_self,0xFF6D6D,FW_BOLD,43;
	end;
	
OnMacro:
	macroDetector = 0;
	isMacroDetecting = 0;
	atcommand "@monsterignore";
	if((checkidle() - macroIdleTimer) < (macroIdleTimerTarget / 1000)){
		announce "คุณไม่ยอมอยู่นิ่ง ๆ จากการตรวจ Macro..",bc_self,0xFF6D6D,FW_BOLD,43;
		tired = cap_value(tired + 100,0,100);
		RefreshDebuff();
	}
	else
	announce "ขอบคุณมาก!",bc_self,0xFF6D6D,FW_BOLD,43;

	end;
	
OnInit:
	.killAmountMacro = 100;		// How many killed to start for detecting macro

	.traderSpawnChance = 100;	// 10000 = 100%
	.baseChance = 100;			// 10000 = 100%
	.staminaDecrease = 20;		// 10000 = 100%
	.monsterLevelDivider = 10;	// Use to increase chance to found THE BOX (Monster Level / n)
	.sameMapAmount = 3;
	$maximumMapLimitation = 5;
	end;
}
