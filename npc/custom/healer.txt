prontera,159,193,5	script	แพทย์	10458,{
	menu "ซ่อมอุปกรณ์สวมใส่ (^BA17F8" + callfunc("F_InsertComma",.repairAllPrice) + "z^000000)",L_RepairAll
	,"รับการรักษาพิเศษ (^BA17F8" + callfunc("F_InsertComma",.foodBuffsPrice) + "z^000000)",L_FoodBuffs
	,"รักษา Debuff จากการหมดสติ (^BA17F8" + ((selfCareDebuff > gettimetick(2)) ? (callfunc("F_InsertComma",.clearDebuffPrice * debuff) + "z^000000)") : "ฟรี^000000)"),L_ClearDebuff;
	
	L_RepairAll:
	menu "ยืนยัน 'ซ่อมอุปกรณ์สวมใส่'",-;
	
	if(Zeny < .repairAllPrice)
	{
		message strcharinfo(0),"ต้องการ " + callfunc("F_InsertComma",.repairAllPrice) + "z";
		end;
	}
	
	Zeny = cap_value(Zeny - .repairAllPrice,0,INT_MAX);
	
	repairall;
	
	message strcharinfo(0),"ซ่อมอุปกรณ์สวมใส่ ทั้งหมดในตัว เรียบร้อยแล้ว";
	
	end;
	
	L_FoodBuffs:
	menu "ยืนยัน 'รับการรักษาพิเศษ'",-;
	
	if(Zeny < .foodBuffsPrice)
	{
		message strcharinfo(0),"ต้องการ " + callfunc("F_InsertComma",.foodBuffsPrice) + "z";
		end;
	}
	
	Zeny = cap_value(Zeny - .foodBuffsPrice,0,INT_MAX);
	
	sc_end SC_POISON;
	sc_end SC_SILENCE;
	sc_end SC_BLIND;
	sc_end SC_CONFUSION;
	sc_end SC_CURSE;
	sc_end SC_Hallucination;
	sc_end SC_BLEEDING;
	sc_end SC_ORCISH;
	sc_end SC_CHANGEUNDEAD;
	sc_end SC_HANDICAPSTATE_DEEPBLIND;
	sc_end SC_HANDICAPSTATE_DEEPSILENCE;
	sc_end SC_HANDICAPSTATE_LASSITUDE;
	sc_end SC_HANDICAPSTATE_CONFLAGRATION;
	sc_end SC_HANDICAPSTATE_MISFORTUNE;
	sc_end SC_HANDICAPSTATE_DEADLYPOISON;
	sc_end SC_HANDICAPSTATE_DEPRESSION;
	sc_end SC_HANDICAPSTATE_HOLYFLAME;
	
	sc_start2 SC_PROMOTE_HEALTH_RESERCH,1800000,1,3;
	sc_start2 SC_ENERGY_DRINK_RESERCH,1800000,1,3;
	sc_start SC_EXTRACT_WHITE_POTION_Z,1800000,20;
	sc_start SC_SAVAGE_STEAK,1800000,20;
	sc_start SC_COCKTAIL_WARG_BLOOD,1800000,20;
	sc_start SC_MINOR_BBQ,1800000,20;
	sc_start SC_SIROMA_ICE_TEA,1800000,20;
	sc_start SC_DROCERA_HERB_STEAMED,1800000,20;
	sc_start SC_PUTTI_TAILS_NOODLES,1800000,20;
	sc_start2 SC_VITATA_500,1800000,20,5;
	sc_start SC_EXTRACT_SALAMINE_JUICE,1800000,10;
	sc_start SC_ADD_ATK_DAMAGE,1800000,15;
	sc_start SC_ADD_MATK_DAMAGE,1800000,15;
	sc_start SC_SPEEDUP1,1800000,50;
	
	percentheal 100,100;
	
	message strcharinfo(0),"ได้รับการรักษาพิเศษ!!";
	
	end;
	
	L_ClearDebuff:
	.@price = .clearDebuffPrice * debuff;
	if (selfCareDebuff > gettimetick(2))
	mes "^11B120ข้าสามารถรักษาอาการ Debuff จากการหมดสติ ให้ท่านได้ฟรี^000000 แต่ท่านต้องรอ ^F81717" + GetTimerWording(selfCareDebuff - gettimetick(2)) + "^000000";
	else if(!debuff){
		if(tired)
		message strcharinfo(0),"ท่านไม่มีอาการ Debuff แต่ท่านเหนื่อย ลองไปที่โรงแรม หรือหาอะไรกินตามเมืองดู!";
		else
		message strcharinfo(0),"ท่านไม่มีอาการ Debuff";
		end;
	}
	else 
	.@free = 1;
	
	if(!.@free){
		next;
		if(Zeny >= .@price)
		menu "ข้าไม่อยากรอ ข้าจะจ่าย ^BA17F8" + callfunc("F_InsertComma",.@price) + "z^000000",-;
		else
		menu "ข้าไม่อยากรอ ข้าจะจ่าย ^BA17F8ด้วย Zeny เท่าที่ข้ามี^000000",-;
		
		if(Zeny < .@price)
		{
			// Try at least 1
			if(Zeny >= .clearDebuffPrice){
				.@smallClearAmount = Zeny / .clearDebuffPrice;
				.@price = .clearDebuffPrice * .@smallClearAmount;
			}
			else{
				message strcharinfo(0),"ต้องการ " + callfunc("F_InsertComma",.@price) + "z";
				
				end;
			}
		}
	}
	
	if(!.@free)
	Zeny = cap_value(Zeny - .@price,0,INT_MAX);
	
	if(!.@smallClearAmount){
		debuff = 0;
		selfCareDebuff = 0;
	}
	else{
		debuff = cap_value(debuff - .@smallClearAmount,0,INT_MAX);
	}
	
	RefreshDebuff();
	
	if(!.@smallClearAmount)
	message strcharinfo(0),"ได้รับการรักษา Debuff จากการหมดสติ";
	else
	message strcharinfo(0),"ได้รับการรักษา Debuff จากการหมดสติบางส่วน";
	
	if(!.@free)
	close;
	else
	end;
	
OnInit:
	.foodBuffsPrice = 1000000;
	.repairAllPrice = 100000;
	.clearDebuffPrice = 10000;
	end;
}

prt_in,60,143,5	duplicate(แพทย์)	แพทย์#HotelDoctor1	10458
prt_in,237,136,5	duplicate(แพทย์)	แพทย์#HotelDoctor2	10458
prt_church,113,106,3	duplicate(แพทย์)	แพทย์#HotelChurch	10458
